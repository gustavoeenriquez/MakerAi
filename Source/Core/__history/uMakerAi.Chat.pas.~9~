// MIT License
//
// Copyright (c) 2013 Gustavo Enríquez - CimaMaker
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// o use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// HE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//
// Nombre: Gustavo Enríquez
// Redes Sociales:
// - Email: gustavoeenriquez@gmail.com
// - Telegram: +57 3128441700
// - LinkedIn: https://www.linkedin.com/in/gustavo-enriquez-3937654a/
// - Youtube: https://www.youtube.com/@cimamaker3945
// - GitHub: https://github.com/gustavoeenriquez/


// --------- CAMBIOS --------------------
// 29/08/2024 - Se adiciona el manejo de response_format = json_schema
// 04/11/2024 - adiciona el manejo de detail como propiedad en mediafile
// 04/11/2024 - adiciona el manejo de TAiChat.Stream_Usage - Estadistica de uso en modo stream OpenAi



// https://platform.openai.com/docs/guides/tools-code-interpreter

unit uMakerAi.Chat;

interface

uses
  System.SysUtils, System.Classes, System.Generics.Collections,
  System.Threading, System.TypInfo, System.Types, System.Net.Mime,
  System.NetConsts, System.NetEncoding, System.Net.URLClient,
  System.Net.HttpClient, System.Net.HttpClientComponent, System.JSON, Rest.JSON,

{$IF CompilerVersion < 35}
  uJSONHelper,
{$ENDIF}
  uMakerAi.ToolFunctions, uMakerAi.Core, uMakerAi.Utils.CodeExtractor;

type
  TAiChatMessage = Class(TObject)
  Private
    FPreviousResponseId: String;
    FWebSearchResponse: TAiWebSearch;
    FReasoning: String;
    FIsTollCallResponse: Boolean;
    procedure SetContent(const Value: String);
    procedure SetRole(const Value: String);
    procedure SetPrompt(const Value: String);
    procedure SetFunctionName(const Value: String);
    procedure SetTollCallId(const Value: String);
    procedure SetTool_calls(const Value: String);
    procedure SetFId(const Value: integer);
    procedure SetCompletion_tokens(const Value: integer);
    procedure SetPrompt_tokens(const Value: integer);
    procedure SetTotal_tokens(const Value: integer);
    procedure SetFPreviousResponseId(const Value: String);
    procedure SetWebSearchResponse(const Value: TAiWebSearch);
    procedure SetReasoning(const Value: String);
    procedure SetIsTollCallResponse(const Value: Boolean);
  Protected
    FRole: String;
    FContent: String;
    FPrompt: String;
    FCompletion_tokens: integer;
    FTotal_tokens: integer;
    FPrompt_tokens: integer;
    FId: integer;
    FTollCallId: String;
    FFunctionName: String;
    FTool_calls: String;
    FMediaFiles: TAiMediaFiles;
  Public
    Constructor Create(aPrompt, aRole: String; aToolCallId: String = ''; aFunctionName: String = '');
    Destructor Destroy; Override;

    Procedure AddMediaFile(aMediaFile: TAiMediaFile);
    Procedure LoadMediaFromFile(aFileName: String);
    Procedure LoadMediaFromStream(aFileName: String; Stream: TMemoryStream);
    Procedure LoadMediaFromBase64(aFileName: String; aBase64: String);

    Function HasUnprocessedItems: Boolean;
    Function GetMediaTranscription: String; // Retorna las transcripciones de todos los archivos de medios

    Function StreamToBase64(Stream: TMemoryStream): String;
    Function ToJSon: TJSonArray; // Convierte el Objeto en un json para enviar al api

    Property Id: integer Read FId Write SetFId;
    Property Role: String read FRole write SetRole;
    Property Content: String read FContent write SetContent;
    Property Prompt: String read FPrompt write SetPrompt;
    Property Prompt_tokens: integer read FPrompt_tokens Write SetPrompt_tokens;
    Property Completion_tokens: integer read FCompletion_tokens Write SetCompletion_tokens;
    Property Total_tokens: integer read FTotal_tokens Write SetTotal_tokens;
    Property TollCallId: String read FTollCallId write SetTollCallId;
    Property FunctionName: String read FFunctionName write SetFunctionName;
    Property Tool_calls: String read FTool_calls write SetTool_calls;
    Property MediaFiles: TAiMediaFiles Read FMediaFiles;
    Property WebSearchResponse: TAiWebSearch read FWebSearchResponse write SetWebSearchResponse;
    Property PreviousResponseId: String read FPreviousResponseId Write SetFPreviousResponseId; // Nueva propiedad
    Property Reasoning: String read FReasoning write SetReasoning;
    Property IsTollCallResponse: Boolean read FIsTollCallResponse write SetIsTollCallResponse;
  End;

  TAiChatMessages = Class(TList<TAiChatMessage>)
  Private
    FNativeInputFiles: TAiFileCategories;
    function GetAsText: String;
    procedure SetAsText(const Value: String);
    procedure SetNativeInputFiles(const Value: TAiFileCategories);
  Protected
  Public
    Function ToJSon: TJSonArray;
    Function ExportChatHistory: TJSonObject;
    Procedure SaveToStream(Stream: TStream);
    Procedure SaveToFile(FileName: String);
    Procedure LoadFromStream(Stream: TStream);
    Procedure LoadFromFile(FileName: String);
    Property AsText: String Read GetAsText Write SetAsText;
    Property NativeInputFiles: TAiFileCategories read FNativeInputFiles write SetNativeInputFiles;
  End;

  TAiChatResponseFormat = (tiaChatRfText, tiaChatRfJson, tiaChatRfJsonSchema);
  TAiChatOnDataEvent = procedure(const Sender: TObject; aMsg: TAiChatMessage; aResponse: TJSonObject; aRole, aText: String) of object;
  TAiChatOnBeforeSendEvent = procedure(const Sender: TObject; var aMsg: TAiChatMessage) of object;
  TAiChatOnInitChatEvent = procedure(const Sender: TObject; aRole: String; Var aText: String; Var aMemory: TJSonObject) of object;
  TAiChatOnMediaFileEvent = Procedure(Const Sender: TObject; Prompt: String; MediaFile: TAiMediaFile; aNativeInputFiles: TAiFileCategories; Var Respuesta: String; Var aProcesado: Boolean) of object;
  TAiChatOnProcessResponseEvent = procedure(const Sender: TObject; LastMsg, ResMsg: TAiChatMessage; var Response: String) of object;

  // Evento callback cuando se utiliza la herramienta tools del chat
  TOnCallToolFunction = Procedure(Sender: TObject; AiToolCall: TAiToolsFunction) of object;

  TAiChat = class(TComponent)
  Private
    FOwner: TObject;
    FApiKey: String;
    FSeed: integer;
    FTool_choice: string;
    FN: integer;
    FTop_p: Double;
    FLogprobs: Boolean;
    FFrequency_penalty: Double;
    FStop: string;
    FLogit_bias: String;
    FTemperature: Double;
    FPresence_penalty: Double;
    FUser: String;
    FMax_tokens: integer;
    FAsynchronous: Boolean;
    FTop_logprobs: String;
    FModel: String;
    FInitialInstructions: TStrings;
    FCompletion_tokens: integer;
    FTotal_tokens: integer;
    FPrompt_tokens: integer;
    FTool_Active: Boolean;
    FUrl: String;
    FResponseTimeOut: integer;
    FOnInitChat: TAiChatOnInitChatEvent;
    FMemory: TStrings;

    // FFunctions: TFunctionActionItems;
    FAiFunctions: TAiFunctions;
    FOnProcessMediaFile: TAiChatOnMediaFileEvent;
    FJsonSchema: TStrings;
    FStream_Usage: Boolean;
    FNativeInputFiles: TAiFileCategories;
    FNativeOutputFiles: TAiFileCategories;
    // FWebSearch: Boolean;
    FOnError: TAiErrorEvent;
    FOnProcessResponse: TAiChatOnProcessResponseEvent;
    FVoice: String;
    Fvoice_format: String;
    FLanguage: string;
    FTranscription_ResponseFormat: string;
    FTranscription_TimestampGranularities: string;
    FChatMediaSupports: TAiChatMediaSupports;
    FReasoningFormat: String;
    FReasoningEffort: String;

    procedure SetApiKey(const Value: String);
    procedure SetFrequency_penalty(const Value: Double);
    procedure SetLogit_bias(const Value: String);
    procedure SetLogprobs(const Value: Boolean);
    procedure SetMax_tokens(const Value: integer);
    procedure SetN(const Value: integer);
    procedure SetPresence_penalty(const Value: Double);
    procedure SetResponse_format(const Value: TAiChatResponseFormat);
    procedure SetSeed(const Value: integer);
    procedure SetStop(const Value: string);
    procedure SetTemperature(const Value: Double);
    procedure SetTool_choice(const Value: string);
    procedure SetTop_p(const Value: Double);
    procedure SetUser(const Value: String);
    procedure SetAsynchronous(const Value: Boolean);
    procedure SetTop_logprobs(const Value: String);
    procedure SetOnReceiveDataEvent(const Value: TAiChatOnDataEvent);
    procedure SetOnReceiveDataEnd(const Value: TAiChatOnDataEvent);
    procedure SetModel(const Value: String);
    procedure SetInitialInstructions(const Value: TStrings);
    procedure SetOnAddMessage(const Value: TAiChatOnDataEvent);
    procedure SetOnCallToolFunction(const Value: TOnCallToolFunction);
    procedure SetTool_Active(const Value: Boolean);
    procedure SetOnBeforeSendMessage(const Value: TAiChatOnBeforeSendEvent);
    procedure SetCompletion_tokens(const Value: integer);
    procedure SetPrompt_tokens(const Value: integer);
    procedure SetTotal_tokens(const Value: integer);
    procedure SetUrl(const Value: String);
    procedure SetLastError(const Value: String);
    procedure SetResponseTimeOut(const Value: integer);
    procedure SetOnInitChat(const Value: TAiChatOnInitChatEvent);
    procedure SetMemory(const Value: TStrings);
    procedure SetJsonSchema(const Value: TStrings);

    procedure SetAiFunctions(const Value: TAiFunctions);
    procedure SetOnProcessMediaFile(const Value: TAiChatOnMediaFileEvent);
    procedure SetStream_Usage(const Value: Boolean);
    procedure SetNativeInputFiles(const Value: TAiFileCategories);
    procedure SetNativeOutputFiles(const Value: TAiFileCategories);
    // procedure SetWebSearch(const Value: Boolean);
    procedure SetOnError(const Value: TAiErrorEvent);
    procedure SetOnProcessResponse(const Value: TAiChatOnProcessResponseEvent);
    procedure SetVoice(const Value: String);
    procedure Setvoice_format(const Value: String);
    procedure SetLanguage(const Value: string);
    procedure SetTranscription_ResponseFormat(const Value: string);
    procedure SetTranscription_TimestampGranularities(const Value: string);
    procedure SetChatMediaSupports(const Value: TAiChatMediaSupports);
    function GetApiKey: String;
    procedure SetReasoningEffort(const Value: String);
    procedure SetReasoningFormat(const Value: String);

  Protected
    FClient: TNetHTTPClient;
    FTmpRole: String;
    FTmpResponseText: String;
    FTmpResponseText1: String;
    FAbort: Boolean;
    FBusy: Boolean;
    FTools: TStrings;
    FLastContent: String;
    FLastPrompt: String;
    FLastError: String;
    FMessages: TAiChatMessages;
    FResponse_format: TAiChatResponseFormat;
    FResponse: TStringStream;
    FOnReceiveDataEvent: TAiChatOnDataEvent;
    FOnReceiveDataEnd: TAiChatOnDataEvent;
    FOnAddMessage: TAiChatOnDataEvent;
    FOnCallToolFunction: TOnCallToolFunction;
    FOnBeforeSendMessage: TAiChatOnBeforeSendEvent;
    Procedure OnInternalReceiveData(const Sender: TObject; AContentLength, AReadCount: Int64; var AAbort: Boolean); Virtual;

    Procedure OnRequestErrorEvent(const Sender: TObject; const AError: string); Virtual;
    Procedure OnRequestExceptionEvent(const Sender: TObject; const AError: Exception); Virtual;
    Procedure OnRequestCompletedEvent(const Sender: TObject; const aResponse: IHTTPResponse); Virtual;

    Function InternalAddMessage(aPrompt, aRole: String; aToolCallId: String; aFunctionName: String): TAiChatMessage; Overload; Virtual;
    Function InternalAddMessage(aPrompt, aRole: String; aMediaFiles: TAiMediaFilesArray): TAiChatMessage; Overload; Virtual;
    Function InternalAddMessage(aMsg: TAiChatMessage): TAiChatMessage; Overload; Virtual;

    function InternalRunSpeechGeneration(ResMsg, AskMsg: TAiChatMessage): String; Virtual;
    function InternalRunImageGeneration(ResMsg, AskMsg: TAiChatMessage): String; Virtual;
    function InternalRunImageVideoGeneration(ResMsg, AskMsg: TAiChatMessage): String; Virtual;
    function InternalRunWebSearch(ResMsg, AskMsg: TAiChatMessage): String; Virtual;

    Function InternalRunCompletions(ResMsg, AskMsg: TAiChatMessage): String; Virtual;
    function InternalRunTranscription(aMediaFile: TAiMediaFile; ResMsg, AskMsg: TAiChatMessage): String; Virtual;
    function InternalRunImageDescription(aMediaFile: TAiMediaFile; ResMsg, AskMsg: TAiChatMessage): String; Virtual;
    function InternalRunPDFDescription(aMediaFile: TAiMediaFile; ResMsg, AskMsg: TAiChatMessage): String; Virtual;

    // Inicializa el json de completions, se saca apaarte porque es complejo
    Function InitChatCompletions: String; Virtual;

    Procedure ParseChat(jObj: TJSonObject; ResMsg: TAiChatMessage); Virtual;
    procedure ParseJsonTranscript(jObj: TJSonObject; ResMsg: TAiChatMessage; aMediaFile: TAiMediaFile);

    Function ExtractToolCallFromJson(jChoices: TJSonArray): TAiToolsFunctions; Virtual;
    Procedure DoCallFunction(ToolCall: TAiToolsFunction); Virtual;
    function GetTools(aToolFormat: TToolFormat): TStrings; virtual;
    Function PrepareSystemMsg: String; Virtual; // Crea el primer mensaje del chat para system, para configurar el asistente
    Procedure DoProcessMediaFile(aPrompt: String; aAiMediaFile: TAiMediaFile; Var Respuesta: String; Var Procesado: Boolean);
    Function AddMessageAndRun(aPrompt, aRole: String; aToolCallId: String; aFunctionName: String): String; Overload;
    procedure DoError(const ErrorMsg: string; E: Exception); virtual;
    Procedure DoProcessResponse(aLastMsg, aResMsg: TAiChatMessage; var aResponse: String);
  Public
    Constructor Create(Sender: TComponent); Override;
    Destructor Destroy; Override;
    Function AddMessageAndRun(aPrompt, aRole: String; aMediaFiles: TAiMediaFilesArray): String; Overload;
    Function AddMessageAndRunMsg(aPrompt, aRole: String; aMediaFiles: TAiMediaFilesArray): TAiChatMessage; Overload;

    Function AddMessage(aPrompt, aRole: String): TAiChatMessage; // Crea un mensaje y lo adiciona a la conversación
    Function NewMessage(aPrompt, aRole: String): TAiChatMessage; // Crea un mensaje pero no lo adiciona a la conversación
    Function Run(AskMsg: TAiChatMessage; ResMsg: TAiChatMessage = Nil): String; Virtual;
    Function GetLastMessage: TAiChatMessage;
    Function RemoveMesage(Msg: TAiChatMessage): Boolean; Overload;
    Function RemoveMesage(IdMsg: integer): Boolean; Overload;
    Procedure AddToMemory(Key, Value: String);
    Procedure RemoveFromMemory(Key: String);
    Procedure NewChat;
    Procedure Abort;
    Class Function GetModels(aApiKey: String; aUrl: String = ''): TStringList; Overload; virtual;
    Function GetModels: TStringList; Overload; Virtual;
    Function GetMessages: TJSonArray; Virtual;

    Function PublicChatToSend: String;

    Function FileCategoriesToString(const ACategories: TAiFileCategories): string;
    Function StringToFileCategories(const AValue: string): TAiFileCategories;

    Function UploadFile(aMediaFile: TAiMediaFile): String; Virtual;
    Function DownLoadFile(aMediaFile: TAiMediaFile): String; Virtual;
    Function CheckFileState(aMediaFile: TAiMediaFile): String; Virtual;
    Function DeleteFile(aMediaFile: TAiMediaFile): String; Virtual;
    function RetrieveFile(aFileId: string): TAiMediaFile; virtual;
    function RetrieveFileList: TAiMediaFiles; virtual;
    Function UploadFileToCache(aMediaFile: TAiMediaFile; aTTL_Seconds: integer = 3600): String; Virtual;

    class function GetDriverName: string; virtual; abstract;
    class procedure RegisterDefaultParams(Params: TStrings); virtual; abstract;
    class function CreateInstance(Sender: TComponent): TAiChat; virtual; abstract;

    Property Messages: TAiChatMessages read FMessages;
    Property LastError: String read FLastError write SetLastError;

  Published
    Property ApiKey: String read GetApiKey write SetApiKey;
    Property Model: String read FModel write SetModel;
    Property Frequency_penalty: Double read FFrequency_penalty write SetFrequency_penalty; // -2 to 2
    Property Logit_bias: String read FLogit_bias write SetLogit_bias;
    // Vacío o entre -100 y 100
    Property Logprobs: Boolean read FLogprobs write SetLogprobs;
    Property Top_logprobs: String read FTop_logprobs write SetTop_logprobs;
    // vacio o between 0 and 5
    Property Max_tokens: integer read FMax_tokens write SetMax_tokens;
    // 0 = null o el máximo
    Property N: integer read FN write SetN;
    // Defaults to 1 How many Chat completion choices to generate for each input message.Note that you will be charged based on the number of generated tokens across all of the choices.Keep n as 1 to minimize costs.
    Property Presence_penalty: Double read FPresence_penalty write SetPresence_penalty; // Defaults to 0 number between - 2.0 and 2.0
    Property Response_format: TAiChatResponseFormat read FResponse_format write SetResponse_format;
    // object Optional an object specifying the format that the model must output.Compatible with gpt - 4 - 1106 - preview and gpt - 3.5 - turbo - 1106.
    Property Seed: integer read FSeed write SetSeed; // 0 no se envía
    Property Stop: string read FStop write SetStop;
    // Array de palabras separado por comas
    Property Asynchronous: Boolean read FAsynchronous write SetAsynchronous;
    Property Temperature: Double read FTemperature write SetTemperature;
    // Defaults to 1  between 0 and 2.
    Property Top_p: Double read FTop_p write SetTop_p;
    // Defaults to 0 si es 0 no se envía,  entre 0 y 1
    // Property Tools: TStrings read GetTools;
    Property Tool_choice: string read FTool_choice write SetTool_choice;
    Property Tool_Active: Boolean read FTool_Active write SetTool_Active;
    Property User: String read FUser write SetUser;
    Property InitialInstructions: TStrings read FInitialInstructions write SetInitialInstructions;
    Property Prompt_tokens: integer read FPrompt_tokens write SetPrompt_tokens;
    Property Completion_tokens: integer read FCompletion_tokens write SetCompletion_tokens;
    Property Total_tokens: integer read FTotal_tokens write SetTotal_tokens;
    Property LastContent: String Read FLastContent;
    Property LastPrompt: String Read FLastPrompt;
    Property Busy: Boolean Read FBusy;
    Property OnReceiveData: TAiChatOnDataEvent read FOnReceiveDataEvent write SetOnReceiveDataEvent;
    Property OnReceiveDataEnd: TAiChatOnDataEvent read FOnReceiveDataEnd write SetOnReceiveDataEnd;
    Property OnAddMessage: TAiChatOnDataEvent read FOnAddMessage write SetOnAddMessage;
    Property OnCallToolFunction: TOnCallToolFunction read FOnCallToolFunction write SetOnCallToolFunction;
    Property OnBeforeSendMessage: TAiChatOnBeforeSendEvent read FOnBeforeSendMessage write SetOnBeforeSendMessage;
    Property OnInitChat: TAiChatOnInitChatEvent read FOnInitChat write SetOnInitChat;
    Property OnProcessResponse: TAiChatOnProcessResponseEvent read FOnProcessResponse write SetOnProcessResponse;
    Property Url: String read FUrl write SetUrl;
    Property ResponseTimeOut: integer read FResponseTimeOut write SetResponseTimeOut;
    Property Memory: TStrings read FMemory Write SetMemory;
    // Property Functions: TFunctionActionItems read FFunctions write FFunctions;
    Property AiFunctions: TAiFunctions read FAiFunctions write SetAiFunctions;
    Property OnProcessMediaFile: TAiChatOnMediaFileEvent read FOnProcessMediaFile write SetOnProcessMediaFile;
    Property JsonSchema: TStrings read FJsonSchema write SetJsonSchema;
    Property Stream_Usage: Boolean read FStream_Usage write SetStream_Usage;
    Property NativeInputFiles: TAiFileCategories read FNativeInputFiles write SetNativeInputFiles;
    Property NativeOutputFiles: TAiFileCategories read FNativeOutputFiles write SetNativeOutputFiles;
    Property ChatMediaSupports: TAiChatMediaSupports read FChatMediaSupports write SetChatMediaSupports;

    Property Voice: String read FVoice write SetVoice;
    Property voice_format: String read Fvoice_format write Setvoice_format;
    // Property WebSearch: Boolean read FWebSearch write SetWebSearch;
    property OnError: TAiErrorEvent read FOnError write SetOnError;
    property Language: string read FLanguage write SetLanguage; // e.g., 'es', 'en', 'es-419'
    property Transcription_ResponseFormat: string read FTranscription_ResponseFormat write SetTranscription_ResponseFormat;
    // 'json', 'text', 'verbose_json', etc.
    property Transcription_TimestampGranularities: string read FTranscription_TimestampGranularities write SetTranscription_TimestampGranularities; // 'word', 'segment', 'word,segment'
    Property ReasoningFormat: String read FReasoningFormat write SetReasoningFormat;
    Property ReasoningEffort: String read FReasoningEffort write SetReasoningEffort;

  end;

  // procedure Register;

implementation

uses uMakerAi.ParamsRegistry;

{ TAiChat }

Const
  GlOpenAIUrl = 'https://api.openai.com/v1/';

procedure TAiChat.Abort;
begin
  FAbort := True;
end;

// --- Este mensaje se envía cuando es una función toll, es indepenciente a los otros dos
function TAiChat.InternalAddMessage(aPrompt, aRole: String; aToolCallId: String; aFunctionName: String): TAiChatMessage;
Var
  Msg: TAiChatMessage;
begin
  Try
    // Adiciona el mensaje a la lista
    Msg := TAiChatMessage.Create(aPrompt, aRole, aToolCallId, aFunctionName);
    Msg.FId := FMessages.Count + 1;
    FMessages.Add(Msg);
    FLastPrompt := aPrompt;

    Result := Msg;

    // ------ al llamar una herramienta tool no se valida un before message debe ser manejado solo por el código -------
    // If Assigned(FOnBeforeSendMessage) then FOnBeforeSendMessage(Self, Msg);

  Finally
  End;
end;

// ------- Metodo para el manejo de mensajes del chat normal
function TAiChat.InternalAddMessage(aPrompt, aRole: String; aMediaFiles: TAiMediaFilesArray): TAiChatMessage;
Var
  Msg: TAiChatMessage;
  MF: TAiMediaFile;
begin

  Try
    // Adiciona el mensaje a la lista
    Msg := TAiChatMessage.Create(aPrompt, aRole);

    If Length(aMediaFiles) > 0 then
    Begin
      For MF in aMediaFiles do
      Begin
        If Assigned(MF) then
          Msg.AddMediaFile(MF);
      End;
      Result := InternalAddMessage(Msg);
    End;

  Finally
  End;
end;

// ------- Metodo para el manejo de mensajes del chat normal
function TAiChat.InternalAddMessage(aMsg: TAiChatMessage): TAiChatMessage;
Var
  TmpMsg: TAiChatMessage;
  MF: TAiMediaFile;
  MensajeInicial: String;
  Respuesta: String;
  Procesado: Boolean;
begin

  If Not Assigned(aMsg) then
    Raise Exception.Create('El parámetro aMsg debe estar instanciado');

  Try

    MensajeInicial := Trim(Self.PrepareSystemMsg);

    // Comienza con las instrucciones iniciales, en cada modelo es diferente
    If (FMessages.Count = 0) and (MensajeInicial <> '') then
    Begin

      TmpMsg := TAiChatMessage.Create(MensajeInicial, 'system');
      TmpMsg.FId := FMessages.Count + 1;
      FMessages.Add(TmpMsg);

      If Assigned(FOnAddMessage) then
        FOnAddMessage(Self, TmpMsg, Nil, 'system', MensajeInicial);
    End;

    // Adiciona el mensaje a la lista
    If Assigned(FMessages) and Assigned(aMsg) then
    Begin
      aMsg.FId := FMessages.Count + 1;
      FMessages.Add(aMsg);
    End;

    If Assigned(FOnAddMessage) then
    Begin
      FOnAddMessage(Self, aMsg, Nil, aMsg.Role, aMsg.Prompt);
    End;

    For MF in aMsg.MediaFiles do
    Begin
      If Assigned(MF) then
      Begin
        Procesado := False;
        DoProcessMediaFile(aMsg.Prompt, MF, Respuesta, Procesado); // Envía el archivo por si lo quiere procesar otra AI especializada, Ej.
        MF.Procesado := Procesado;
        MF.Transcription := Respuesta;
        // Guarda las transcripciones en los MediaFile,  luego construye la respuesta definitiva con todos los mediafiles
      End;
    End;

    FLastPrompt := aMsg.Prompt; // aqui lleva el Prompt Inicial + la conversión de los MediaFiles a texto si el usuario lo permite

    if Assigned(FOnAddMessage) then
      FOnAddMessage(Self, aMsg, Nil, aMsg.Role, aMsg.Prompt);

    If Assigned(FOnBeforeSendMessage) then
      FOnBeforeSendMessage(Self, aMsg);

    Result := aMsg;

  Finally
  End;
end;

function TAiChat.InternalRunCompletions(ResMsg, AskMsg: TAiChatMessage): String;
Var
  ABody: String;
  sUrl: String;
  Res: IHTTPResponse;
  St: TStringStream;
  FHeaders: TNetHeaders;
  jObj: TJSonObject;
begin

  FBusy := True; // Marca como ocupado al sistema
  FAbort := False; // Inicia como no en abort
  FLastError := '';
  FLastContent := '';
  FLastPrompt := '';
  St := TStringStream.Create('', TEncoding.UTF8);

  sUrl := FUrl + 'chat/completions';

  Try
    FHeaders := [TNetHeader.Create('Authorization', 'Bearer ' + ApiKey)];
    FClient.ContentType := 'application/json';

    // Comienza con las instrucciones iniciales y le adiciona cada 20 mensajes para evitar que se olvide

    ABody := InitChatCompletions;

    St.WriteString(ABody);
    St.Position := 0;

{$IFDEF APIDEBUG}
    St.SaveToFile('c:\temp\peticion.txt');
    St.Position := 0;
{$ENDIF}
    FResponse.Clear;
    FResponse.Position := 0;

    Res := FClient.Post(sUrl, St, FResponse, FHeaders);

    FResponse.Position := 0;
{$IFDEF APIDEBUG}
    FResponse.SaveToFile('c:\temp\respuesta.txt');
    FResponse.Position := 0;
{$ENDIF}
    FLastContent := '';

    // If Self.Asynchronous = False then
    If FClient.Asynchronous = False then
    Begin
      if Res.StatusCode = 200 then
      Begin
        jObj := TJSonObject(TJSonObject.ParseJSONValue(Res.ContentAsString));
        Try
          FBusy := False;
          ParseChat(jObj, ResMsg);
          Result := FLastContent;

        Finally
          FreeAndNil(jObj);
        End;
      End
      else
      begin
        Raise Exception.CreateFmt('Error Received: %d, %s', [Res.StatusCode, Res.ContentAsString]);
      end;
    End;
  Finally
    If FClient.Asynchronous = False then
      St.Free; // Esto no funciona en multiarea, así que se libera cuando no lo es.
  End;
end;

function TAiChat.InternalRunImageDescription(aMediaFile: TAiMediaFile; ResMsg, AskMsg: TAiChatMessage): String;
begin
  Raise Exception.Create('No está implementada la función runImageDescription en este modelo');
end;

function TAiChat.InternalRunImageGeneration(ResMsg, AskMsg: TAiChatMessage): String;
begin
  Raise Exception.Create('No está implementada la función runImageGeneration en este modelo');
end;

function TAiChat.InternalRunImageVideoGeneration(ResMsg, AskMsg: TAiChatMessage): String;
begin
  Raise Exception.Create('No está implementada la función runVideoGeneration en este modelo');
end;

function TAiChat.InternalRunPDFDescription(aMediaFile: TAiMediaFile; ResMsg, AskMsg: TAiChatMessage): String;
begin
  Raise Exception.Create('No está implementada la función runVideoGeneration en este modelo');
end;

function TAiChat.InternalRunSpeechGeneration(ResMsg, AskMsg: TAiChatMessage): String;
var
  LUrl, LModel, LVoice, LResponseFormat: string;
  LBodyStream: TStringStream;
  LResponseStream: TMemoryStream;
  LHeaders: TNetHeaders; // <--- Variable ya declarada, ahora la usaremos
  LResponse: IHTTPResponse;
  LJsonObject: TJSonObject;
  S, LErrorResponse: string;
  LResponseMsg: TAiChatMessage;
  LNewAudioFile: TAiMediaFile;
begin
  Result := ''; // La función Run devuelve el texto, que en este caso es vacío.
  FBusy := True;
  FLastError := '';
  FLastContent := '';
  FLastPrompt := AskMsg.Prompt;

  // 2. Preparar parámetros para la API de TTS
  LUrl := Url + 'audio/speech';
  LModel := TAiChatFactory.Instance.GetBaseModel(GetDriverName, Model);
  LVoice := Self.Voice; // Usamos la propiedad del componente
  LResponseFormat := Self.voice_format; // Usamos la propiedad del componente

  // 3. Construir y ejecutar la petición
  LJsonObject := TJSonObject.Create;
  LBodyStream := nil;
  LResponseStream := TMemoryStream.Create;
  try
    LJsonObject.AddPair('model', LModel);
    LJsonObject.AddPair('input', AskMsg.Prompt);
    LJsonObject.AddPair('voice', LVoice);

    If voice_format.IsEmpty then
      voice_format := 'wav';

    LJsonObject.AddPair('response_format', voice_format);


    // LBodyStream := TStringStream.Create(UTF8ToString(LJsonObject.ToString), TEncoding.UTF8);
    // LBodyStream := TStringStream.Create(LJsonObject.ToString, TEncoding.UTF8);

    S := LJsonObject.ToString;

    LBodyStream := TStringStream.Create;
    LBodyStream.WriteString(S);

    LHeaders := [TNetHeader.Create('Authorization', 'Bearer ' + ApiKey)];
    FClient.ContentType := 'application/json';

    LBodyStream.Position := 0;
{$IFDEF APIDEBUG}
    LBodyStream.SaveToFile('c:\temp\peticionAudio.json.txt');
    LBodyStream.Position := 0;
{$ENDIF}
    LResponse := FClient.Post(LUrl, LBodyStream, LResponseStream, LHeaders);
    // LResponse := FClient.Post(LUrl, LBodyStream, Nil, LHeaders);

    // 4. Procesar la respuesta
    if LResponse.StatusCode = 200 then
    begin
      // ... (el resto del código de procesamiento de la respuesta es correcto)
      LNewAudioFile := TAiMediaFile.Create;
      try
        LResponseStream.Position := 0;

{$IFDEF APIDEBUG}
        LResponseStream.SaveToFile('c:\temp\respuestavoice.txt');
        LResponseStream.Position := 0;
{$ENDIF}
        LNewAudioFile.LoadFromStream('generated_audio.' + LResponseFormat, LResponseStream);
        LResponseMsg := TAiChatMessage.Create(AskMsg.Prompt, 'assistant');
        LResponseMsg.MediaFiles.Add(LNewAudioFile);
        LResponseMsg.Id := FMessages.Count + 1;
        FMessages.Add(LResponseMsg);
        if Assigned(FOnReceiveDataEnd) then
          FOnReceiveDataEnd(Self, LResponseMsg, nil, 'assistant', '');
      except
        LNewAudioFile.Free;
        raise;
      end;
    end
    else
    begin
      // ... (el resto del código de manejo de errores es correcto)
      LResponseStream.Position := 0;
      LErrorResponse := TStreamReader.Create(LResponseStream).ReadToEnd;
      FLastError := Format('Error generando audio: %d, %s', [LResponse.StatusCode, LErrorResponse]);
      DoError(FLastError, nil);
    end;
  finally
    LJsonObject.Free;
    LBodyStream.Free;
    LResponseStream.Free;
  end;

  FBusy := False;
end;

function TAiChat.InternalRunTranscription(aMediaFile: TAiMediaFile; ResMsg, AskMsg: TAiChatMessage): String;
var
  Body: TMultipartFormData;
  Client: TNetHTTPClient;
  Headers: TNetHeaders;
  sUrl: String;
  Res: IHTTPResponse;
  LResponseStream: TMemoryStream;
  LTempStream: TMemoryStream;
  LResponseObj: TJSonObject;
  Granularities: TStringList; // Para procesar las granularidades
  I: integer;
  LModel: String;
begin
  Result := '';
  if not Assigned(aMediaFile) or (aMediaFile.Content.Size = 0) then
    raise Exception.Create('Se necesita un archivo de audio con contenido para la transcripción.');

  sUrl := Url + 'audio/transcriptions';

  Client := TNetHTTPClient.Create(Self);

{$IF CompilerVersion >= 35}
  Client.SynchronizeEvents := False;
{$ENDIF}
  LResponseStream := TMemoryStream.Create;
  Body := TMultipartFormData.Create;
  Granularities := TStringList.Create;
  LModel := TAiChatFactory.Instance.GetBaseModel(GetDriverName, Model);

  try
    Headers := [TNetHeader.Create('Authorization', 'Bearer ' + ApiKey)];

    // Crear un stream temporal para pasarlo al formulario multipart
    LTempStream := TMemoryStream.Create;
    aMediaFile.Content.Position := 0;
    LTempStream.LoadFromStream(aMediaFile.Content);
    LTempStream.Position := 0;

    // --- 1. CONSTRUCCIÓN DEL BODY MULTIPART CON PARÁMETROS GENÉRICOS ---
    Body.AddStream('file', LTempStream, aMediaFile.FileName, aMediaFile.MimeType);

    // Modelo: usa el modelo principal si es de transcripción, si no, usa un default.
    Body.AddField('model', LModel); // Default seguro

    if not AskMsg.Prompt.IsEmpty then
      Body.AddField('prompt', AskMsg.Prompt);

    // Formato de respuesta (genérico, como string)
    if not Self.Transcription_ResponseFormat.IsEmpty then
      Body.AddField('response_format', Self.Transcription_ResponseFormat)
    else
      Body.AddField('response_format', 'json'); // Default a JSON si no se especifica

    // Parámetros opcionales
    if not Self.Language.IsEmpty then
      Body.AddField('language', Self.Language);

    // Usamos la propiedad de Temperatura existente en el componente
    if Self.Temperature > 0 then
      Body.AddField('temperature', FormatFloat('0.0', Self.Temperature));

    // Timestamps Granularities (procesamos la cadena)
    if not Self.Transcription_TimestampGranularities.IsEmpty then
    begin
      // Dividimos la cadena por comas
      Granularities.CommaText := Self.Transcription_TimestampGranularities;
      for I := 0 to Granularities.Count - 1 do
      begin
        // Añadimos cada granularidad como un campo separado con '[]'
        Body.AddField('timestamp_granularities[]', Trim(Granularities[I]));
      end;
    end;

    // TODO : TODAVÍA NO ESTÁ LISTO PARA UTILIZAR LA TRANSCRIPCIÓN EN MODO ASCINCRÓNICO, Falta implementar a futuro
    // Streaming
    // if Self.Asynchronous then
    // Body.AddField('stream', 'true');

    // --- 2. EJECUCIÓN DE LA PETICIÓN POST ---

    // (La lógica de streaming/síncrono se mantiene igual)
    begin
      Res := Client.Post(sUrl, Body, LResponseStream, Headers);

      if Res.StatusCode = 200 then
      begin

        LResponseObj := TJSonObject.ParseJSONValue(Res.ContentAsString) as TJSonObject;

        If Not Assigned(LResponseObj) then
        Begin
          LResponseObj := TJSonObject.Create(TJSonPair.Create('text', Res.ContentAsString));
        End;

        try
          // Aquí llamas al procedimiento de parseo de transcripciones
          ParseJsonTranscript(LResponseObj, ResMsg, aMediaFile);
        finally
          LResponseObj.Free;
        end;

        Result := ResMsg.Prompt;
      end
      else
      begin
        Raise Exception.CreateFmt('Error en la transcripción: %d, %s', [Res.StatusCode, Res.ContentAsString]);
      end;
    end;

  finally
    Body.Free;
    Client.Free;
    LResponseStream.Free;
    // Granularities.Free;
    // LTempStream es propiedad de Body
  end;
end;

function TAiChat.InternalRunWebSearch(ResMsg, AskMsg: TAiChatMessage): String;
begin
  Raise Exception.Create('No está implementada la función InternalRunWebSearch en este modelo');
end;

function TAiChat.AddMessage(aPrompt, aRole: String): TAiChatMessage;
begin
  InternalAddMessage(aPrompt, aRole, []);
end;

function TAiChat.AddMessageAndRun(aPrompt, aRole: String; aMediaFiles: TAiMediaFilesArray): String;
begin
  InternalAddMessage(aPrompt, aRole, aMediaFiles);
  Result := Run(Nil, Nil);
end;

function TAiChat.AddMessageAndRunMsg(aPrompt, aRole: String; aMediaFiles: TAiMediaFilesArray): TAiChatMessage;
begin
  if FAsynchronous = False then
  Begin
    AddMessageAndRun(aPrompt, aRole, aMediaFiles);
    Result := Self.GetLastMessage; // Retorna el mensaje
  End
  Else
    Raise Exception.Create('Esta función no es compatible con el modo asincrónico')
end;

function TAiChat.AddMessageAndRun(aPrompt, aRole: String; aToolCallId: String; aFunctionName: String): String;
begin
  InternalAddMessage(aPrompt, aRole, aToolCallId, aFunctionName);
  Result := Run(Nil, Nil);
end;

procedure TAiChat.AddToMemory(Key, Value: String);
begin
  FMemory.AddPair(Key, Value);
end;

function TAiChat.CheckFileState(aMediaFile: TAiMediaFile): String;
begin
  // Revisa el estado de un archivo
end;

constructor TAiChat.Create(Sender: TComponent);
begin
  inherited;
  FOwner := Sender;
  FMessages := TAiChatMessages.Create;
  FTools := TStringList.Create;
  FMemory := TStringList.Create;
  FJsonSchema := TStringList.Create;
  FInitialInstructions := TStringList.Create;
  // FFunctions := TFunctionActionItems.Create(Self, TFunctionActionItem);

  FResponse := TStringStream.Create('', TEncoding.UTF8);
  FClient := TNetHTTPClient.Create(Self);
{$IF CompilerVersion >= 35}
  FClient.SynchronizeEvents := False;
  FClient.OnRequestException := Self.OnRequestExceptionEvent;
{$ENDIF}
  FClient.OnReceiveData := Self.OnInternalReceiveData;
  FClient.OnRequestError := Self.OnRequestErrorEvent;
  FClient.OnRequestCompleted := Self.OnRequestCompletedEvent;
  FClient.ResponseTimeOut := 60000;

  FNativeInputFiles := [];
  FNativeOutputFiles := [];

  FModel := 'gpt4-o';
  FN := 1;
  FResponse_format := TAiChatResponseFormat.tiaChatRfText;
  FTemperature := 1;
  FUser := 'user';
  FInitialInstructions.Text := 'Eres un asistente muy útil y servicial';
  FMax_tokens := 3000;
  FUrl := GlOpenAIUrl;
  FTop_p := 1;
  FResponseTimeOut := 60000;
  FStream_Usage := False; // Envia la estadistica de uso por token
  FTool_choice := 'auto';
end;

function TAiChat.DeleteFile(aMediaFile: TAiMediaFile): String;
begin
  // Permite borrar un archivo previamente subido al llm
end;

destructor TAiChat.Destroy;
begin
  FResponse.Free;
  FTools.Free;
  FClient.Free;
  FMemory.Free;
  FJsonSchema.Free;
  inherited;
end;

procedure TAiChat.DoCallFunction(ToolCall: TAiToolsFunction);
Var
  Funcion: TFunctionActionItem;
  Handle: Boolean;
begin

  If AiFunctions.DoCallFunction(ToolCall) then
  Begin
    // Si ejecutó la función
  End
  Else
  Begin // Ejecuta la función por defecto en el componente TAiChat
    If Assigned(FOnCallToolFunction) then
      FOnCallToolFunction(Self, ToolCall)
  End;

  {
    If Assigned(FAiFunctions) then // Si está asignado el componente, busca la función en el componente
    Funcion := FAiFunctions.Functions.GetFunction(ToolCall.Name);
    // Else // Si no está asignado el componente, lo busca directamente en las funciones locales
    // Funcion := FFunctions.GetFunction(ToolCall.Name);

    If Assigned(Funcion) then
    Begin
    Funcion.OnAction(Self, Funcion, ToolCall.Name, ToolCall, Handle);
    If Handle = False then
    Begin
    If Assigned(FOnCallToolFunction) then
    FOnCallToolFunction(Self, ToolCall)
    End;
    End
    Else
    Begin
    If Assigned(FOnCallToolFunction) then
    FOnCallToolFunction(Self, ToolCall)
    End;
  }

end;

procedure TAiChat.DoError(const ErrorMsg: string; E: Exception);
begin
  if Assigned(FOnError) then
    FOnError(Self, ErrorMsg, E, Nil);
end;

procedure TAiChat.DoProcessMediaFile(aPrompt: String; aAiMediaFile: TAiMediaFile; Var Respuesta: String; Var Procesado: Boolean);
begin
  Procesado := False;

  If Assigned(FOnProcessMediaFile) then
  Begin
    Respuesta := '';
    FOnProcessMediaFile(Self, aPrompt, aAiMediaFile, Self.NativeInputFiles, Respuesta, Procesado);
    aAiMediaFile.Procesado := Procesado;
    aAiMediaFile.Transcription := Respuesta;
  End;
end;

procedure TAiChat.DoProcessResponse(aLastMsg, aResMsg: TAiChatMessage; var aResponse: String);
begin
  If Assigned(FOnProcessResponse) then
    FOnProcessResponse(Self, aLastMsg, aResMsg, aResponse);
end;

function TAiChat.DownLoadFile(aMediaFile: TAiMediaFile): String;
begin

end;

function TAiChat.ExtractToolCallFromJson(jChoices: TJSonArray): TAiToolsFunctions;
Var
  jObj, Msg, jFunc, Arg: TJSonObject;
  JVal, JVal1, jValToolCall: TJSonValue;
  Fun: TAiToolsFunction;
  JToolCalls: TJSonArray;
  Nom, Valor: String;
  I: integer;
begin
  Result := TAiToolsFunctions.Create;

  For JVal1 in jChoices do
  Begin
    Msg := TJSonObject(JVal1).GetValue<TJSonObject>('message');

    // Msg.TryGetValue<TJSonValue>('tool_calls', JValToolCall);

    If Msg.TryGetValue<TJSonValue>('tool_calls', jValToolCall) and (jValToolCall is TJSonArray) and Msg.TryGetValue<TJSonArray>('tool_calls', JToolCalls) then
    Begin
      For JVal in JToolCalls do
      Begin
        jObj := TJSonObject(JVal);
        If jObj.GetValue<String>('type') = 'function' then
        Begin
          jObj := TJSonObject(JVal);
          Fun := TAiToolsFunction.Create;
          Fun.Id := jObj.GetValue<String>('id');
          Fun.Tipo := jObj.GetValue<String>('type');

          If jObj.TryGetValue<TJSonObject>('function', jFunc) then
          Begin
            // Fun.Name := jObj.GetValue<TJSonObject>('function').GetValue<String>('name');
            Fun.Name := jFunc.GetValue<String>('name');

            Fun.Arguments := jObj.GetValue<TJSonObject>('function').GetValue<String>('arguments');
          End;

          Try
            If (Fun.Arguments <> '') and (Fun.Arguments <> '{}') then
            Begin
              Arg := TJSonObject(TJSonObject.ParseJSONValue(Fun.Arguments));
              If Assigned(Arg) then
              Begin
                For I := 0 to Arg.Count - 1 do
                Begin
                  Nom := Arg.Pairs[I].JsonString.Value;
                  Valor := Arg.Pairs[I].JsonValue.Value;
                  Fun.Params.Values[Nom] := Valor;
                End;
              End;
            End;

          Except
            // Si no hay parámetros no marca error
          End;

          Result.Add(Fun.Id, Fun);
        End;
      End;
    End;
  End;
end;

function TAiChat.GetApiKey: String;
begin
  // Si está en modo de diseño, simplemente retorna el valor tal cual
  if (csDesigning in ComponentState) or (csDestroying in ComponentState) then
  begin
    Result := FApiKey;
    Exit;
  end;

  // En modo de ejecución
  if (FApiKey <> '') and (Copy(FApiKey, 1, 1) = '@') then
    // Retorna el valor de la variable de entorno, quitando el '@'
    Result := GetEnvironmentVariable(Copy(FApiKey, 2, Length(FApiKey)))
  else
    Result := FApiKey;
end;

function TAiChat.GetLastMessage: TAiChatMessage;
begin
  Result := Nil;
  If FMessages.Count > 0 then
    Result := FMessages[FMessages.Count - 1];
end;

function TAiChat.GetMessages: TJSonArray;
begin
  Result := FMessages.ToJSon;
end;

function TAiChat.GetModels: TStringList;
begin
  Result := GetModels(ApiKey, Url);
end;

class function TAiChat.GetModels(aApiKey, aUrl: String): TStringList;
Var
  Client: TNetHTTPClient;
  Headers: TNetHeaders;
  Res: IHTTPResponse;
  Response: TStringStream;
  sUrl, EndPointUrl: String;
  jRes: TJSonObject;
  JArr: TJSonArray;
  JVal: TJSonValue;
  sModel: string;
  CustomModels: TArray<string>;
  I: integer;
begin
  Result := TStringList.Create;

  If aUrl <> '' then
    EndPointUrl := aUrl
  Else
    EndPointUrl := GlOpenAIUrl;

  Client := TNetHTTPClient.Create(Nil);
  Response := TStringStream.Create('', TEncoding.UTF8);
  sUrl := EndPointUrl + 'models';

  Try
    Headers := [TNetHeader.Create('Authorization', 'Bearer ' + aApiKey)];
    Client.ContentType := 'application/json';

    Res := Client.Get(sUrl, Response, Headers);
    // Response.SaveToFile('c:\temp\models.json.txt');

    if Res.StatusCode = 200 then
    Begin
      jRes := TJSonObject(TJSonObject.ParseJSONValue(Res.ContentAsString));
      If jRes.TryGetValue<TJSonArray>('data', JArr) then
      Begin
        For JVal in JArr do
        Begin
          sModel := JVal.GetValue<String>('id');
          If sModel <> '' then
            Result.Add(sModel);
        End;
      End;

      // Agregar modelos personalizados
      CustomModels := TAiChatFactory.Instance.GetCustomModels(Self.GetDriverName);

      {
        for I := Low(CustomModels) to High(CustomModels) do
        begin
        if not Result.Contains(CustomModels[I]) then
        Result.Add(CustomModels[I]);
        end;
      }

      for I := Low(CustomModels) to High(CustomModels) do
      begin
{$IF CompilerVersion <= 35.0}
        if Result.IndexOf(CustomModels[I]) = -1 then
          Result.Add(CustomModels[I]);
{$ELSE}
        if not Result.Contains(CustomModels[I]) then
          Result.Add(CustomModels[I]);
{$ENDIF}
      end;

    End
    else
    begin
      Raise Exception.CreateFmt('Error Received: %d, %s', [Res.StatusCode, Res.ContentAsString]);
    end;
  Finally
    Client.Free;
    Response.Free;
  End;
end;

function TAiChat.GetTools(aToolFormat: TToolFormat): TStrings;
begin
  If Assigned(FAiFunctions) and FTool_Active then // Si está asignado el componente lo obtiene del componente
  Begin
    FTools.Text := FAiFunctions.GetTools(aToolFormat);
    Result := FTools;
  End
  Else
  Begin
    FTools.Text := '';
    Result := FTools;
  End;
end;

function TAiChat.InitChatCompletions: String;
Var
  AJSONObject, jToolChoice: TJSonObject;
  JArr: TJSonArray;
  JStop: TJSonArray;
  Lista: TStringList;
  I: integer;
  LAsincronico: Boolean;
  Res, LModel: String;
begin

  If FUser = '' then
    FUser := 'user';

  LModel := TAiChatFactory.Instance.GetBaseModel(GetDriverName, Model);

  If LModel = '' then
    LModel := 'gpt-4o';

  // Las funciones no trabajan en modo ascincrono
  LAsincronico := Self.Asynchronous and (not Self.Tool_Active);

  // estas líneas no hacen falta, se dejan como gúia para proximas implementaciones
  // LastMsg := Messages.Last;
  { If Assigned(LastMsg) then
    Begin
    LAsincronico := LAsincronico and (Not(LastMsg.VisionUrls.Count > 0) or (LastMsg.VisionBase64.Count > 0));
    End;
  }

  FClient.Asynchronous := LAsincronico;

  AJSONObject := TJSonObject.Create;
  Lista := TStringList.Create;

  Try
    AJSONObject.AddPair('stream', TJSONBool.Create(LAsincronico));

    If Tool_Active and (Trim(GetTools(TToolFormat.tfOpenAi).Text) <> '') then
    Begin

{$IF CompilerVersion < 35}
      JArr := TJSONUtils.ParseAsArray(GetTools(TToolFormat.tfOpenAi).Text);
{$ELSE}
      JArr := TJSonArray(TJSonArray.ParseJSONValue(GetTools(TToolFormat.tfOpenAi).Text));
{$ENDIF}
      If Not Assigned(JArr) then
        Raise Exception.Create('La propiedad Tools están mal definido, debe ser un JsonArray');
      AJSONObject.AddPair('tools', JArr);

      If (Trim(FTool_choice) <> '') then
      Begin

{$IF CompilerVersion < 35}
        jToolChoice := TJSONUtils.ParseAsObject(FTool_choice);
{$ELSE}
        jToolChoice := TJSonObject(TJSonArray.ParseJSONValue(FTool_choice));
{$ENDIF}
        If Assigned(jToolChoice) then
          AJSONObject.AddPair('tools_choice', jToolChoice);
      End;
    End;

    AJSONObject.AddPair('messages', GetMessages); // FMessages.ToJSon);
    AJSONObject.AddPair('model', LModel);
    AJSONObject.AddPair('user', User);

    If (FResponse_format = tiaChatRfJsonSchema) then
    Begin
      AJSONObject.AddPair('response_format', TJSonObject.Create.AddPair('type', 'json_schema'))
    End
    Else If { LAsincronico or } (FResponse_format = tiaChatRfJson) then
      AJSONObject.AddPair('response_format', TJSonObject.Create.AddPair('type', 'json_object'))
    Else If (FResponse_format = tiaChatRfText) then
      AJSONObject.AddPair('response_format', TJSonObject.Create.AddPair('type', 'text'))
    Else
      AJSONObject.AddPair('response_format', TJSonObject.Create.AddPair('type', 'text'));

    If FReasoningEffort <> '' then // OpenAi reasoning effort
    Begin
      var
      jeffort := TJSonObject.Create;
      jeffort.AddPair('effort', FReasoningEffort);
      AJSONObject.AddPair('reasoning', jeffort);

      AJSONObject.AddPair('max_completion_tokens', TJSONNumber.Create(FMax_tokens));
    End
    Else
      AJSONObject.AddPair('max_tokens', FMax_tokens);

    Lista.CommaText := FStop;
    If Lista.Count > 0 then
    Begin
      JStop := TJSonArray.Create;
      For I := 0 to Lista.Count - 1 do
        JStop.Add(Lista[I]);
      AJSONObject.AddPair('stop', JStop);
    End;

    If FLogprobs = True then
    Begin
      If FLogit_bias <> '' then
        AJSONObject.AddPair('logit_bias', TJSONNumber.Create(FLogit_bias));

      AJSONObject.AddPair('logprobs', TJSONBool.Create(FLogprobs));

      If FTop_logprobs <> '' then
        AJSONObject.AddPair('top_logprobs', TJSONNumber.Create(FTop_logprobs));
    End;

    If Seed > 0 then
      AJSONObject.AddPair('seed', TJSONNumber.Create(FSeed));

    if tcm_WebSearch in FChatMediaSupports then
    begin
      // La API de OpenAI espera un objeto para las opciones, incluso si está vacío.
      Var
      jWebSearchOptions := TJSonObject.Create;
      AJSONObject.AddPair('web_search_options', jWebSearchOptions);
    end
    Else
    Begin
      If FTop_p <> 0 then
        AJSONObject.AddPair('top_p', TJSONNumber.Create(FTop_p));

      AJSONObject.AddPair('temperature', TJSONNumber.Create(Trunc(FTemperature * 100) / 100));
      AJSONObject.AddPair('frequency_penalty', TJSONNumber.Create(Trunc(FFrequency_penalty * 100) / 100));
      AJSONObject.AddPair('presence_penalty', TJSONNumber.Create(Trunc(FPresence_penalty * 100) / 100));
      AJSONObject.AddPair('n', TJSONNumber.Create(FN));

    End;

    Res := UTF8ToString(AJSONObject.ToJSon);
    Res := StringReplace(Res, '\/', '/', [rfReplaceAll]);
    Result := StringReplace(Res, '\r\n', '', [rfReplaceAll]);
  Finally
    AJSONObject.Free;
    Lista.Free;
  End;
end;

procedure TAiChat.NewChat;
Var
  I: integer;
begin
  For I := FMessages.Count - 1 downto 0 do
  Begin
    FMessages[I].Free;
    FMessages.Delete(I);
  End;
  FMessages.Clear;
end;

function TAiChat.NewMessage(aPrompt, aRole: String): TAiChatMessage;
begin
  Result := TAiChatMessage.Create(aPrompt, aRole);
end;

procedure AppendTextToFile(const aText: string; const aFileName: string = 'c:\temp\stream.txt');
var
  FileStream: TFileStream;
  TextBytes: TBytes;
  Directory: string;
begin
  // Extraer el directorio del archivo
  Directory := ExtractFilePath(aFileName);

  // Crear el directorio si no existe
  if not DirectoryExists(Directory) then
    ForceDirectories(Directory);

  try
    // Si el archivo existe, abrirlo en modo append (al final)
    // Si no existe, crearlo
    if FileExists(aFileName) then
      FileStream := TFileStream.Create(aFileName, fmOpenWrite or fmShareDenyWrite)
    else
      FileStream := TFileStream.Create(aFileName, fmCreate or fmShareDenyWrite);

    try
      // Posicionarse al final del archivo para append
      FileStream.Seek(0, soFromEnd);

      // Convertir el string a bytes (UTF-8)
      TextBytes := TEncoding.UTF8.GetBytes(aText + sLineBreak);

      // Escribir los bytes al archivo
      FileStream.WriteBuffer(TextBytes, Length(TextBytes));

    finally
      FileStream.Free;
    end;

  except
    on E: Exception do
      raise Exception.Create('Error al escribir al archivo: ' + E.Message);
  end;
end;

procedure TAiChat.OnInternalReceiveData(const Sender: TObject; AContentLength, AReadCount: Int64; var AAbort: Boolean);
Var
  jObj, Delta: TJSonObject;
  sJson, Value, Role1: String;
  P: integer;
  Msg: TAiChatMessage;
begin

  If FClient.Asynchronous = False then
    Exit;

  AAbort := FAbort;

  If FAbort = True then
  Begin
    FBusy := False;
    If Assigned(FOnReceiveDataEnd) then
      FOnReceiveDataEnd(Self, Nil, Nil, 'system', 'abort');
  End;

  Try

    AppendTextToFile(FResponse.DataString);
    FTmpResponseText := FTmpResponseText + FResponse.DataString;
    FTmpResponseText1 := FTmpResponseText1 + FResponse.DataString;

    FResponse.Clear;
    FResponse.Position := 0;

    If Copy(FTmpResponseText, 1, 5) = 'data:' then
      FTmpResponseText := Copy(FTmpResponseText, 6, Length(FTmpResponseText));

    Repeat
      P := Pos('data:', FTmpResponseText);
      If P > 0 then
      Begin
        sJson := Trim(Copy(FTmpResponseText, 1, P - 1));
        FTmpResponseText := Copy(FTmpResponseText, P + 6, Length(FTmpResponseText));
      End
      Else
      Begin
        If Trim(FTmpResponseText) = '[DONE]' then // Terminó el proceso
        Begin
          sJson := Trim(FTmpResponseText);
          FTmpResponseText := '';
        End
        Else
          sJson := '';
      End;

      If sJson = '[DONE]' then // Terminó el proceso
      Begin
        sJson := '';
        Msg := TAiChatMessage.Create(FLastContent, FTmpRole);
        Msg.FId := FMessages.Count + 1;
        FMessages.Add(Msg);
        FBusy := False;

        If Assigned(FOnReceiveDataEnd) then
          FOnReceiveDataEnd(Self, Msg, Nil, FTmpRole, FLastContent);
      End
      Else If sJson <> '' then
      Begin
        jObj := TJSonObject(TJSonObject.ParseJSONValue(sJson));

        Try
          If Assigned(jObj) then
          Begin
{$IF CompilerVersion >= 35} // Delphi 11 Alexandria y posteriores
            Delta := jObj.GetValue<TJSonArray>('choices')[0].GetValue<TJSonObject>('delta');
            Value := '';
            Delta.TryGetValue<String>('content', Value);
            Delta.TryGetValue<String>('role', Role1);
{$ELSE}     // Para versiones anteriores como Delphi 10
            var
              ChoicesArray: TJSonArray;
            var
              FirstChoice: TJSonObject;

            ChoicesArray := jObj.GetValueAsArray('choices');
            if Assigned(ChoicesArray) and (ChoicesArray.Count > 0) then
            begin
              FirstChoice := ChoicesArray.GetItemAsObject(0);
              if Assigned(FirstChoice) then
              begin
                Delta := FirstChoice.GetValueAsObject('delta');
                if Assigned(Delta) then
                begin
                  Value := Delta.GetValueAsString('content');
                  Role1 := Delta.GetValueAsString('role');
                end
                else
                begin
                  Value := '';
                  Role1 := '';
                end;
              end
              else
              begin
                Delta := nil;
                Value := '';
                Role1 := '';
              end;
            end
            else
            begin
              Delta := nil;
              Value := '';
              Role1 := '';
            end;

{$ENDIF}
            If Role1 <> '' then
              FTmpRole := Role1;

            FLastContent := FLastContent + Value;

            If (Value <> '') and Assigned(FOnReceiveDataEvent) then
            Begin
              Value := StringReplace(Value, #$A, sLineBreak, [rfReplaceAll]);
              FOnReceiveDataEvent(Self, Nil, jObj, FTmpRole, Value);
            End;
          End;
        Finally
          jObj.Free;
        End;
      End;

    Until sJson = '';

  Except

  End;
end;

procedure TAiChat.OnRequestCompletedEvent(const Sender: TObject; const aResponse: IHTTPResponse);
begin
  If Assigned(FOnError) then
  Begin
    If Assigned(aResponse) and ((aResponse.StatusCode < 200) or (aResponse.StatusCode > 299)) then
      FOnError(Self, aResponse.ContentAsString, Nil, aResponse);
  End;
end;

procedure TAiChat.OnRequestErrorEvent(const Sender: TObject; const AError: string);
begin
  If Assigned(FOnError) then
    FOnError(Self, AError, Nil, Nil);
end;

procedure TAiChat.OnRequestExceptionEvent(const Sender: TObject; const AError: Exception);
begin
  If Assigned(FOnError) then
    FOnError(Self, AError.Message, AError, Nil);

end;
{
  procedure TAiChat.ParseChat(jObj: TJSonObject; ResMsg: TAiChatMessage);
  Var
  choices: TJSonArray;
  JItem, jMessage, uso: TJSonObject;
  JVal, JToolCallsValue: TJSonValue;
  aPrompt_tokens, aCompletion_tokens, aTotal_tokens: integer;
  Role, Respuesta, sReasoning, sToolCalls, sRes: String;
  ToolMsg, AskMsg: TAiChatMessage;
  LFunciones: TAiToolsFunctions;
  ToolCall: TAiToolsFunction;
  TaskList: array of ITask;
  I, NumTasks: integer;
  Clave: String;
  begin
  // 1. EXTRAER INFORMACIÓN BÁSICA DE LA RESPUESTA JSON
  // ---------------------------------------------------------------------------
  Model := jObj.GetValue('model').Value;
  if jObj.TryGetValue('usage', uso) then
  begin
  uso.TryGetValue<integer>('prompt_tokens', aPrompt_tokens);
  uso.TryGetValue<integer>('completion_tokens', aCompletion_tokens);
  uso.TryGetValue<integer>('total_tokens', aTotal_tokens);
  end
  else
  begin
  aPrompt_tokens := 0;
  aCompletion_tokens := 0;
  aTotal_tokens := 0;
  end;

  AskMsg := GetLastMessage; // Obtiene el mensaje del usuario que originó esta respuesta.

  // 2. PARSEAR EL ARRAY 'CHOICES' PARA OBTENER EL CONTENIDO
  // ---------------------------------------------------------------------------
  jObj.TryGetValue<TJSonArray>('choices', choices);

  Respuesta := '';
  sToolCalls := '';
  Role := 'assistant'; // Default role

  if Assigned(choices) then
  begin
  For JVal in choices do
  Begin
  JItem := TJSonObject(JVal);
  jMessage := JItem.GetValue<TJSonObject>('message');
  Role := jMessage.GetValue<String>('role');

  // Parsea información adicional como 'reasoning' si existe (para Groq, DeepSeek, etc.)
  if jMessage.TryGetValue<String>('reasoning', sReasoning) then
  ResMsg.Reasoning := sReasoning
  else if jMessage.TryGetValue<String>('reasoning_content', sReasoning) then
  ResMsg.Reasoning := sReasoning;

  // Obtiene el contenido de texto (puede estar vacío en una llamada a función)
  jMessage.TryGetValue<String>('content', sRes);
  if Not Trim(sRes).IsEmpty then
  Respuesta := Trim(Respuesta + sLineBreak + sRes);

  // Obtiene las llamadas a funciones si existen
  if jMessage.TryGetValue<TJSonValue>('tool_calls', JToolCallsValue) and (JToolCallsValue is TJSonArray) then
  begin
  sToolCalls := TJSonArray(JToolCallsValue).Format;
  end;

  End;
  end;

  Respuesta := Trim(Respuesta);

  // 3. EVENTO PARA PROCESAMIENTO PERSONALIZADO DE LA RESPUESTA
  // ---------------------------------------------------------------------------
  // Permite al usuario modificar el texto de la respuesta antes de que se guarde.
  DoProcessResponse(AskMsg, ResMsg, Respuesta);

  // 4. POBLAR EL OBJETO ResMsg Y ACTUALIZAR CONTADORES
  // ---------------------------------------------------------------------------
  // Preparamos el objeto de mensaje de respuesta del asistente con toda la info.
  ResMsg.Role := Role;
  ResMsg.FPrompt := Respuesta;
  ResMsg.FContent := Respuesta; // Asignamos a ambos por consistencia
  ResMsg.FTool_calls := sToolCalls;
  ResMsg.FPrompt_tokens := aPrompt_tokens;
  ResMsg.FCompletion_tokens := aCompletion_tokens;
  ResMsg.FTotal_tokens := aTotal_tokens;

  // Actualizamos los contadores globales del componente
  Self.FLastContent := Respuesta;
  FPrompt_tokens := FPrompt_tokens + aPrompt_tokens;
  FCompletion_tokens := FCompletion_tokens + aCompletion_tokens;
  FTotal_tokens := FTotal_tokens + aTotal_tokens;

  // 5. PROCESAR EL FLUJO DE FUNCTION CALLING O RESPUESTA DE TEXTO
  // ---------------------------------------------------------------------------
  LFunciones := ExtractToolCallFromJson(choices);
  try
  if LFunciones.Count > 0 then
  begin
  // --- A. FLUJO DE FUNCTION CALLING ---

  // PASO CRÍTICO 1: Añadir el mensaje del 'assistant' al historial.
  // Este mensaje contiene las 'tool_calls' que la API solicita ejecutar.
  // El historial ahora es: [..., user_message, assistant_message_with_tool_calls]

  ResMsg.Id := FMessages.Count + 1;
  FMessages.Add(ResMsg);

  if Assigned(FOnAddMessage) then
  FOnAddMessage(Self, ResMsg, jObj, ResMsg.Role, ResMsg.Prompt);

  // PASO 2: Ejecutar las funciones solicitadas en paralelo.
  NumTasks := LFunciones.Count;
  SetLength(TaskList, NumTasks);
  I := 0;
  for Clave in LFunciones.Keys do
  begin
  ToolCall := LFunciones[Clave];
  TaskList[I] := TTask.Create(
  procedure
  begin
  try
  DoCallFunction(ToolCall);
  except
  on E: Exception do
  TThread.Queue(nil,
  procedure
  begin
  DoError('Error in "' + ToolCall.Name + '"', E);
  end);
  end;
  end);
  TaskList[I].Start;
  Inc(I);
  end;
  TTask.WaitForAll(TaskList);

  // PASO 3: Añadir los resultados de las funciones como mensajes de tipo 'tool'.
  // El historial ahora es: [..., user, assistant, tool, (otro tool...)]
  for Clave in LFunciones.Keys do
  begin
  ToolCall := LFunciones[Clave];
  ToolMsg := TAiChatMessage.Create(ToolCall.Response, 'tool', ToolCall.Id, ToolCall.Name);
  ToolMsg.FId := FMessages.Count + 1;
  FMessages.Add(ToolMsg);
  if Assigned(FOnAddMessage) then
  FOnAddMessage(Self, ToolMsg, nil, ToolMsg.Role, ToolMsg.Prompt);
  end;

  // PASO 4: Realizar la segunda llamada a la API con el historial completo.
  // Pasamos (Nil, Nil) porque los mensajes ya están en el historial, y esperamos
  // que la API nos devuelva la respuesta final en lenguaje natural.
  Var
  aMsg := TAiChatMessage.Create('', '');

  aMsg.IsTollCallResponse := True;

  Self.Run(Nil, aMsg);

  ResMsg.Role := aMsg.Role;
  ResMsg.Prompt := aMsg.Prompt;
  ResMsg.Content := '';
  end
  else
  begin
  // --- B. FLUJO DE RESPUESTA DE TEXTO NORMAL ---

  // El mensaje ResMsg ya tiene el contenido de texto. Lo añadimos al historial.
  If ResMsg.IsTollCallResponse = False then
  Begin
  ResMsg.Id := FMessages.Count + 1;
  FMessages.Add(ResMsg);
  End;

  // Señalamos que la interacción ha terminado.
  FBusy := False;
  if Assigned(FOnAddMessage) then
  FOnAddMessage(Self, ResMsg, jObj, ResMsg.Role, ResMsg.Prompt);
  if Assigned(FOnReceiveDataEnd) then
  FOnReceiveDataEnd(Self, ResMsg, jObj, ResMsg.Role, Respuesta);
  end;
  finally
  LFunciones.Free;
  end;
  end;

}

procedure TAiChat.ParseChat(jObj: TJSonObject; ResMsg: TAiChatMessage);
Var
  choices: TJSonArray;
  JItem: TJSonObject;
  JVal: TJSonValue;
  JToolCallsValue: TJSonValue;
  jMessage: TJSonObject;
  uso: TJSonObject;
  aPrompt_tokens, aCompletion_tokens, aTotal_tokens: integer;
  Role, Respuesta, sReasoning: String;
  ToolMsg, AskMsg: TAiChatMessage;
  // Msg: TAiChatMessage;
  LFunciones: TAiToolsFunctions;
  ToolCall: TAiToolsFunction;

  TaskList: array of ITask;
  I, NumTasks: integer;
  Clave, sToolCalls, sRes: String;

  Code: TMarkdownCodeExtractor;
  CodeFile: TCodeFile;
  CodeFiles: TCodeFileList;
  MF: TAiMediaFile;
  St: TStringStream;

begin

  // Id := JObj.GetValue('id').Value;
  // IdObject := JObj.GetValue('object').Value;
  // IdCreate := JObj.GetValue('created').GetValue<String>;
  Var
  ModelVersion := jObj.GetValue('model').Value;
  uso := jObj.GetValue('usage') as TJSonObject;
  aPrompt_tokens := uso.GetValue<integer>('prompt_tokens');
  aCompletion_tokens := uso.GetValue<integer>('completion_tokens');
  aTotal_tokens := uso.GetValue<integer>('total_tokens');

  AskMsg := GetLastMessage; // Obtiene la pregunta, ya que ResMsg se adiciona a la lista si no hay errores.

  jObj.TryGetValue<TJSonArray>('choices', choices);

  For JVal in choices do
  Begin
    JItem := TJSonObject(JVal);
    jMessage := JItem.GetValue<TJSonObject>('message');
    Role := jMessage.GetValue<String>('role');

    if jMessage.TryGetValue<String>('reasoning', sReasoning) then // Groq
    begin
      ResMsg.Reasoning := sReasoning;
    end
    Else if jMessage.TryGetValue<String>('reasoning_content', sReasoning) then // DeepSeek
    begin
      ResMsg.Reasoning := sReasoning;
    end;

    jMessage.TryGetValue<String>('content', sRes);

    If Not Trim(sRes).IsEmpty then
      Respuesta := Trim(Respuesta + sLineBreak + sRes)
    Else
      Respuesta := Trim(Respuesta + sLineBreak + sReasoning);

    sToolCalls := '';
    If jMessage.TryGetValue<TJSonValue>('tool_calls', JToolCallsValue) and Assigned(JToolCallsValue) and (JToolCallsValue is TJSonArray) then
    Begin
      sToolCalls := TJSonArray(JToolCallsValue).Format;
    End;
  End;

  Respuesta := Trim(Respuesta);

  Self.FLastContent := Trim(Self.FLastContent + sLineBreak + Respuesta);
  FPrompt_tokens := FPrompt_tokens + aPrompt_tokens;
  FCompletion_tokens := FCompletion_tokens + aCompletion_tokens;
  FTotal_tokens := FTotal_tokens + aTotal_tokens;

  if sToolCalls.IsEmpty then // Si es una respuesta normal adiciona los datos al ResMsg
  Begin
    ResMsg.Role := Role;
    ResMsg.FTool_calls := sToolCalls;
    ResMsg.FPrompt := ResMsg.FPrompt + Respuesta;
    ResMsg.FPrompt_tokens := ResMsg.FPrompt_tokens + aPrompt_tokens;
    ResMsg.FCompletion_tokens := ResMsg.FCompletion_tokens + aCompletion_tokens;
    ResMsg.FTotal_tokens := ResMsg.FTotal_tokens + aTotal_tokens;
    DoProcessResponse(AskMsg, ResMsg, Respuesta);
  End
  Else // Si tiene toolcall lo adiciona y ejecuta nuevamente el run para obtener la respuesta
  Begin
    Var
    Msg := TAiChatMessage.Create(Respuesta, Role);
    Msg.FTool_calls := sToolCalls;
    Msg.FId := FMessages.Count + 1;
    FMessages.Add(Msg);
  End;

  LFunciones := ExtractToolCallFromJson(choices);

  Try
    If LFunciones.Count > 0 then
    Begin

      NumTasks := LFunciones.Count;
      SetLength(TaskList, NumTasks);
      // Ajusta el tamaño del array para el número de tareas

      I := 0;
      For Clave in LFunciones.Keys do
      Begin
        ToolCall := LFunciones[Clave];
        ToolCall.ResMsg := ResMsg; // Se pasan los mensajes por si desean procesarlos
        ToolCall.AskMsg := AskMsg;

        TaskList[I] := TTask.Create(
          procedure
          begin
            Try
              DoCallFunction(ToolCall);
            Except
              On E: Exception do
              Begin

                TThread.Queue(nil,
                  procedure
                  begin
                    DoError('Error in "' + ToolCall.Name + '"', E);
                  end);

              End;
            End;
          end);
        TaskList[I].Start;
        Inc(I);

      End;
      TTask.WaitForAll(TaskList);

      For Clave in LFunciones.Keys do
      Begin
        ToolCall := LFunciones[Clave];
        ToolMsg := TAiChatMessage.Create(ToolCall.Response, 'tool', ToolCall.Id, ToolCall.Name);
        ToolMsg.FId := FMessages.Count + 1;
        FMessages.Add(ToolMsg);
      End;

      Self.Run(Nil, ResMsg);
      ResMsg.Content := '';

    End
    Else
    Begin
      FBusy := False;

      // Si está marcado que genere archivos tomará el resultado y extraerá los archivos y los adicona como mediafiles a ResMsg

      If tfc_textFile in NativeOutputFiles then
      Begin
        Code := TMarkdownCodeExtractor.Create;
        Try

          CodeFiles := Code.ExtractCodeFiles(Respuesta);
          For CodeFile in CodeFiles do
          Begin
            St := TStringStream.Create(CodeFile.Code);
            Try
              St.Position := 0;

              MF := TAiMediaFile.Create;
              MF.LoadFromStream('file.' + CodeFile.FileType, St);
              ResMsg.MediaFiles.Add(MF);
            Finally
              St.Free;
            End;

          End;
        Finally
          Code.Free;
        End;
      End;

      DoProcessResponse(AskMsg, ResMsg, Respuesta);

      ResMsg.Prompt := Respuesta;

      If Assigned(FOnReceiveDataEnd) then
        FOnReceiveDataEnd(Self, ResMsg, jObj, Role, Respuesta);
    End;
  Finally
    LFunciones.Free;
  End;
end;

procedure TAiChat.ParseJsonTranscript(jObj: TJSonObject; ResMsg: TAiChatMessage; aMediaFile: TAiMediaFile);
var
  // JSON parsing
  jUsage, jInputTokenDetails: TJSonObject;
  jArrWords, jArrSegments: TJSonArray;

  // Datos extraídos
  sTextoTranscrito, sTextoWords, sTextoSegments: String;
  aTotal_tokens, aInput_tokens, aOutput_tokens: integer;
  aText_tokens, aAudio_tokens: integer; // Tokens detallados del input

begin
  sTextoTranscrito := '';
  aTotal_tokens := 0;
  aInput_tokens := 0;
  aOutput_tokens := 0;
  aText_tokens := 0;
  aAudio_tokens := 0;

  if not jObj.TryGetValue<string>('text', sTextoTranscrito) then
  begin
    FLastError := 'La respuesta de la API no contiene el campo "text" con la transcripción.';
    DoError(FLastError, nil);
    FBusy := False; // Asumiendo que usas FBusy como en ParseChat
    Exit;
  end;

  If jObj.TryGetValue<TJSonArray>('words', jArrWords) then
    sTextoWords := jArrWords.Format;

  If jObj.TryGetValue<TJSonArray>('segments', jArrSegments) then
    sTextoSegments := jArrSegments.Format;

  // --- 3. EXTRACCIÓN DE DATOS DE USO (TOKENS) ---
  // El objeto 'usage' podría no venir, así que lo manejamos de forma segura.
  if jObj.TryGetValue<TJSonObject>('usage', jUsage) then
  begin
    // Extraemos los tokens principales
    jUsage.TryGetValue<integer>('total_tokens', aTotal_tokens);
    jUsage.TryGetValue<integer>('input_tokens', aInput_tokens); // Costo del audio (y prompt si hubo)
    jUsage.TryGetValue<integer>('output_tokens', aOutput_tokens); // Costo del texto generado

    // Extraemos los detalles de los tokens de entrada (sub-objeto)
    // Esto nos dice cuántos tokens correspondieron al audio y cuántos a un posible prompt de texto.
    if jUsage.TryGetValue<TJSonObject>('input_token_details', jInputTokenDetails) then
    begin
      jInputTokenDetails.TryGetValue<integer>('text_tokens', aText_tokens);
      jInputTokenDetails.TryGetValue<integer>('audio_tokens', aAudio_tokens);
    end;
  end;

  // --- 4. ACTUALIZACIÓN DEL ESTADO DEL COMPONENTE ---
  // Actualizamos los contadores de tokens globales, sumando los de esta llamada.
  Self.Total_tokens := Self.Total_tokens + aTotal_tokens;
  Self.Prompt_tokens := Self.Prompt_tokens + aInput_tokens; // 'input' equivale a 'prompt'
  Self.Completion_tokens := Self.Completion_tokens + aOutput_tokens; // 'output' equivale a 'completion'

  // Guardamos el resultado principal
  // Self.FLastContent := sTextoTranscrito;

  If Trim(sTextoWords + sLineBreak + sTextoSegments) <> '' then
  Begin
    aMediaFile.Transcription := Trim(sTextoWords + sLineBreak + sTextoSegments);
    aMediaFile.Detail := Trim(sTextoTranscrito);
    ResMsg.Prompt := Trim(ResMsg.Prompt + aMediaFile.Transcription);
    ResMsg.Content := ResMsg.Content + sLineBreak + aMediaFile.Detail;
  End
  Else
  Begin
    aMediaFile.Transcription := sTextoTranscrito;
    ResMsg.Prompt := Trim(ResMsg.Prompt + sLineBreak + sTextoTranscrito);
    ResMsg.Content := Trim(ResMsg.Content + sLineBreak + sTextoTranscrito);
  End;

  ResMsg.Prompt_tokens := ResMsg.Prompt_tokens + aInput_tokens;
  ResMsg.Completion_tokens := ResMsg.Completion_tokens + aOutput_tokens;
  ResMsg.Total_tokens := ResMsg.Total_tokens + aTotal_tokens;
end;

function TAiChat.PrepareSystemMsg: String;
Var
  S, Key, Val, MensajeInicial: String;
  I: integer;
  JMemory: TJSonObject;
begin
  // Si el formato de respuesta es Json, siempre debe llevar en la instrucción que el formato sea json
  If Self.Response_format = TAiChatResponseFormat.tiaChatRfJson then
    S := 'Responde en formato json'
  Else
    S := '';

  MensajeInicial := FInitialInstructions.Text + sLineBreak + S;

  JMemory := TJSonObject.Create;
  Try
    Try
      For I := 0 to FMemory.Count - 1 do
      Begin
        Key := FMemory.KeyNames[I];
        Val := FMemory.Values[Key];
        JMemory.AddPair(Key, Val);
      End;
    Except
      ON E: Exception do
      Begin
        Raise Exception.Create('El formato de memoria debe ser Key=Value, no está bien configurado');
      End;
    End;

    If Assigned(FOnInitChat) then // Da la oportunidad de inicializar el chat con parametros adicionales como la memoria
      FOnInitChat(Self, 'system', MensajeInicial, JMemory);

    If Trim(MensajeInicial) <> '' then
      Result := MensajeInicial;

    If Length(Trim(JMemory.Format)) > 10 then
      Result := Result + sLineBreak + 'Para Recordar= ' + JMemory.Format;
  Finally
    JMemory.Free;
  End;
end;

function TAiChat.PublicChatToSend: String;
begin
  Result := InitChatCompletions;
end;

procedure TAiChat.RemoveFromMemory(Key: String);
begin
  FMemory.Values[Key] := '';
end;

function TAiChat.RemoveMesage(IdMsg: integer): Boolean;
Var
  I: integer;
  Msg: TAiChatMessage;
begin
  For I := 0 to FMessages.Count - 1 do
  Begin
    Msg := FMessages[I];
    If Msg.Id = IdMsg then
    Begin
      FMessages.Remove(Msg);
      Break;
    End;
  End;
  Result := True;
end;

function TAiChat.RetrieveFile(aFileId: string): TAiMediaFile;
begin

end;

function TAiChat.RetrieveFileList: TAiMediaFiles;
begin

end;

function TAiChat.Run(AskMsg: TAiChatMessage; ResMsg: TAiChatMessage): String;
Var
  MF: TAiMediaFile;
begin

  Result := '';
  // Inicializa el mensaje de respuesta, pero solo lo adiciona a la lista una vez todo esté procesado.
  If Not Assigned(ResMsg) then
    ResMsg := TAiChatMessage.Create('', 'assistant');

  Try

    // Si el mensaje se pasa al run directamente lo adiciona a la lista de mensajes
    If Assigned(AskMsg) then
      InternalAddMessage(AskMsg);

    Try

      // Obtiene el último mensaje que corresponde a la solicitud
      if not Assigned(AskMsg) then
        AskMsg := GetLastMessage;

      // ResMsg.Prompt := AskMsg.Prompt;

      // Si existen mensajes sin procesar intentará procesarlos antes de pasarlo al chat
      if Assigned(AskMsg) and (AskMsg.HasUnprocessedItems) then
      begin

        for MF in AskMsg.MediaFiles do
        begin
          // Verifica que el medio esté dentro de los NativeInputFiles que puede procesar
          If MF.FileCategory in NativeInputFiles then
          Begin

            // Si InternalRunCompletions no soporta el manejo de este archvio se ejecuta por aquí
            if (not(Tcm_Audio in ChatMediaSupports)) and (MF.FileCategory = Tfc_Audio) and (not MF.Procesado) then
            begin
              // Internamente se va construyendo la respuesta
              InternalRunTranscription(MF, ResMsg, AskMsg);
            end
            else if (not(Tcm_pdf in ChatMediaSupports)) and (MF.FileCategory = Tfc_pdf) and (not MF.Procesado) then
            begin
              // Internamente se va construyendo la respuesta
              InternalRunPDFDescription(MF, ResMsg, AskMsg);
            end
            Else
              // Si InternalRunCompletions no soporta el manejo de este archvio se ejecuta por aquí
              if (not(Tcm_Image in ChatMediaSupports)) and (MF.FileCategory = Tfc_Image) and (not MF.Procesado) and (Not(Tfc_Video in NativeOutputFiles)) then
              begin
                // Internamente se va construyendo la respuesta
                InternalRunImageDescription(MF, ResMsg, AskMsg);
              end
              Else if (Not(Tcm_Video in ChatMediaSupports)) and (Tfc_Video in NativeOutputFiles) then
              begin
                // Internamente se va construyendo la respuesta
                InternalRunImageVideoGeneration(ResMsg, AskMsg);
              end
              Else
              Begin
                InternalRunCompletions(ResMsg, AskMsg);
                If FClient.Asynchronous = True then // Si es ascincrono se adiciona en el evento ondataeventreceive
                Begin
                  ResMsg.Free;
                  ResMsg := Nil;
                End;
              End;
          end
          Else
          Begin
            InternalRunCompletions(ResMsg, AskMsg);
            If FClient.Asynchronous = True then // Si es ascincrono se adiciona en el evento ondataeventreceive
            Begin
              ResMsg.Free;
              ResMsg := Nil;
            End;
          End;

        end;
      end
      Else
      Begin

        // La generación de archivos no requiere MediaFiles de entrada, solo la configuración de los inputfiles
        // Si tiene Tfc_Chat indica que lo procesa directamente el InternalRunCompletions de lo contrario lo
        // hace la función especializada para cada uno. En este caso solo se procesa el prompt y no se pueden
        // generar varios archivos de salida.

        if (Not(Tcm_Image in ChatMediaSupports)) and (Tfc_Image in NativeOutputFiles) then
        begin
          // Internamente se va construyendo la respuesta
          InternalRunImageGeneration(ResMsg, AskMsg);
        end
        Else if (Not(Tcm_Audio in ChatMediaSupports)) and (Tfc_Audio in NativeOutputFiles) then
        begin
          // Internamente se va construyendo la respuesta
          InternalRunSpeechGeneration(ResMsg, AskMsg);
        end
        Else if (Not(Tcm_Video in ChatMediaSupports)) and (Tfc_Video in NativeOutputFiles) then
        begin
          // Internamente se va construyendo la respuesta
          InternalRunImageVideoGeneration(ResMsg, AskMsg);
        end
        Else if (Not(tcm_WebSearch in ChatMediaSupports)) and (Tfc_WebSearch in NativeInputFiles) then
        begin
          // Internamente se va construyendo la respuesta
          InternalRunWebSearch(ResMsg, AskMsg);
        end
        Else
        Begin
          InternalRunCompletions(ResMsg, AskMsg);
          If FClient.Asynchronous = True then // Si es ascincrono se adiciona en el evento ondataeventreceive
          Begin
            ResMsg.Free;
            ResMsg := Nil;
          End;

        End;
      End;

      // Solo si llega hasta aquí lo adiciona a la lista, de lo contrario lo elimina en el error
      // Si es la respuesta de un llamado a una función no se guarda aquí, se guarda en el mensaje correspondiente

      If Assigned(ResMsg) then
        Result := ResMsg.Prompt;

      If (AskMsg.Role <> 'tool') and (AskMsg.TollCallId = '') then
      Begin
        If Assigned(ResMsg) then
        Begin
          ResMsg.Id := FMessages.Count + 1;
          FMessages.Add(ResMsg);
        End;
      End;

    Except
      On E: Exception do
      Begin
        If Assigned(ResMsg) then
          ResMsg.Free;

        DoError(E.Message, E);
      End;
    End;

  Finally

  End;

end;

function TAiChat.RemoveMesage(Msg: TAiChatMessage): Boolean;
begin
  If FMessages.ItemValue(Msg) >= 0 then
    Result := (FMessages.Remove(Msg) <> -1)
  else
    Result := False;
end;

procedure TAiChat.SetAiFunctions(const Value: TAiFunctions);
begin
  FAiFunctions := Value;
end;

procedure TAiChat.SetApiKey(const Value: String);
begin
  FApiKey := Value;
end;

procedure TAiChat.SetAsynchronous(const Value: Boolean);
begin
  FAsynchronous := Value;
  FClient.Asynchronous := Value;
end;

procedure TAiChat.SetChatMediaSupports(const Value: TAiChatMediaSupports);
begin
  FChatMediaSupports := Value;
end;

procedure TAiChat.SetCompletion_tokens(const Value: integer);
begin
  FCompletion_tokens := Value;
end;

procedure TAiChat.SetFrequency_penalty(const Value: Double);
begin
  FFrequency_penalty := Value;
end;

procedure TAiChat.SetInitialInstructions(const Value: TStrings);
begin
  FInitialInstructions.Text := Value.Text;
end;

procedure TAiChat.SetJsonSchema(const Value: TStrings);
begin
  FJsonSchema.Assign(Value);
end;

procedure TAiChat.SetLanguage(const Value: string);
begin
  FLanguage := Value;
end;

procedure TAiChat.SetLastError(const Value: String);
begin
  FLastError := Value;
end;

procedure TAiChat.SetLogit_bias(const Value: String);
begin
  FLogit_bias := Value;
end;

procedure TAiChat.SetLogprobs(const Value: Boolean);
begin
  FLogprobs := Value;
end;

procedure TAiChat.SetMax_tokens(const Value: integer);
begin
  FMax_tokens := Value;
end;

procedure TAiChat.SetMemory(const Value: TStrings);
begin
  FMemory.Text := Value.Text;
end;

procedure TAiChat.SetModel(const Value: String);
begin
  FModel := Value;
end;

procedure TAiChat.SetN(const Value: integer);
begin
  FN := Value;
end;

procedure TAiChat.SetNativeInputFiles(const Value: TAiFileCategories);
begin
  FNativeInputFiles := Value;
  FMessages.NativeInputFiles := Value;
end;

procedure TAiChat.SetNativeOutputFiles(const Value: TAiFileCategories);
begin
  FNativeOutputFiles := Value;
end;

procedure TAiChat.SetOnAddMessage(const Value: TAiChatOnDataEvent);
begin
  FOnAddMessage := Value;
end;

procedure TAiChat.SetOnBeforeSendMessage(const Value: TAiChatOnBeforeSendEvent);
begin
  FOnBeforeSendMessage := Value;
end;

procedure TAiChat.SetOnCallToolFunction(const Value: TOnCallToolFunction);
begin
  FOnCallToolFunction := Value;
end;

procedure TAiChat.SetOnError(const Value: TAiErrorEvent);
begin
  FOnError := Value;
end;

procedure TAiChat.SetOnInitChat(const Value: TAiChatOnInitChatEvent);
begin
  FOnInitChat := Value;
end;

procedure TAiChat.SetOnProcessMediaFile(const Value: TAiChatOnMediaFileEvent);
begin
  FOnProcessMediaFile := Value;
end;

procedure TAiChat.SetOnProcessResponse(const Value: TAiChatOnProcessResponseEvent);
begin
  FOnProcessResponse := Value;
end;

procedure TAiChat.SetOnReceiveDataEnd(const Value: TAiChatOnDataEvent);
begin
  FOnReceiveDataEnd := Value;
end;

procedure TAiChat.SetOnReceiveDataEvent(const Value: TAiChatOnDataEvent);
begin
  FOnReceiveDataEvent := Value;
end;

procedure TAiChat.SetPresence_penalty(const Value: Double);
begin
  FPresence_penalty := Value;
end;

procedure TAiChat.SetPrompt_tokens(const Value: integer);
begin
  FPrompt_tokens := Value;
end;

procedure TAiChat.SetReasoningEffort(const Value: String);
begin
  FReasoningEffort := Value;
end;

procedure TAiChat.SetReasoningFormat(const Value: String);
begin
  FReasoningFormat := Value;
end;

procedure TAiChat.SetResponseTimeOut(const Value: integer);
begin
  If Value < 1000 then
    FResponseTimeOut := 1000
  Else
    FResponseTimeOut := Value;

  FClient.ResponseTimeOut := FResponseTimeOut;
end;

procedure TAiChat.SetResponse_format(const Value: TAiChatResponseFormat);
begin
  FResponse_format := Value;
end;

procedure TAiChat.SetSeed(const Value: integer);
begin
  FSeed := Value;
end;

procedure TAiChat.SetStop(const Value: string);
begin
  FStop := Value;
end;

procedure TAiChat.SetStream_Usage(const Value: Boolean);
begin
  FStream_Usage := Value;
end;

procedure TAiChat.SetTemperature(const Value: Double);
begin
  FTemperature := Value;
end;

procedure TAiChat.SetTool_Active(const Value: Boolean);
begin
  FTool_Active := Value;
end;

procedure TAiChat.SetTool_choice(const Value: string);
begin
  FTool_choice := Value;
end;

procedure TAiChat.SetTop_logprobs(const Value: String);
begin
  FTop_logprobs := Value;
end;

procedure TAiChat.SetTop_p(const Value: Double);
Var
  TmpVal: Double;
begin
  If Value > 1 then
    TmpVal := 1
  Else If Value < 0.1 then
    TmpVal := 0.1
  Else
    TmpVal := Value;

  FTop_p := TmpVal;
end;

procedure TAiChat.SetTotal_tokens(const Value: integer);
begin
  FTotal_tokens := Value;
end;

procedure TAiChat.SetTranscription_ResponseFormat(const Value: string);
begin
  FTranscription_ResponseFormat := Value;
end;

procedure TAiChat.SetTranscription_TimestampGranularities(const Value: string);
begin
  FTranscription_TimestampGranularities := Value;
end;

procedure TAiChat.SetUrl(const Value: String);
begin
  If Value <> '' then
    FUrl := Value
  Else
    FUrl := GlOpenAIUrl;
end;

procedure TAiChat.SetUser(const Value: String);
begin
  FUser := Value;
end;

procedure TAiChat.SetVoice(const Value: String);
begin
  FVoice := Value;
end;

procedure TAiChat.Setvoice_format(const Value: String);
begin
  Fvoice_format := Value;
end;

{ procedure TAiChat.SetWebSearch(const Value: Boolean);
  begin
  FWebSearch := Value;
  end;
}

Function TAiChat.FileCategoriesToString(const ACategories: TAiFileCategories): string;
var
  Category: TAiFileCategory;
  Parts: TStringDynArray;
begin
  SetLength(Parts, 0);
  if ACategories = [] then
    Exit('');

  for Category := Low(TAiFileCategory) to High(TAiFileCategory) do
  begin
    if Category in ACategories then
      Parts := Parts + [GetEnumName(TypeInfo(TAiFileCategory), Ord(Category))];
  end;
  Result := String.Join(',', Parts);
End;

Function TAiChat.StringToFileCategories(const AValue: string): TAiFileCategories;
var
  CategoryName: string;
  EnumValue: integer;
begin
  Result := []; // Empezamos con un set vacío
  if AValue.IsEmpty then
    Exit;

  // Usamos Split de TStringHelper para dividir por comas
  for CategoryName in AValue.Split([',']) do
  begin
    try
      // Obtenemos el valor ordinal del enum a partir de su nombre
      EnumValue := GetEnumValue(TypeInfo(TAiFileCategory), Trim(CategoryName));
      // Incluimos el valor en el set resultante
      Include(Result, TAiFileCategory(EnumValue));
    except
      // Opcional: Ignorar nombres de categoría no válidos o lanzar una excepción
      // Aquí simplemente los ignoramos.
    end;
  end;
End;

function TAiChat.UploadFile(aMediaFile: TAiMediaFile): String;
begin
  // Se reescribe para subir un archivo al modelo y retornar la URI del archivo en el modelo
end;

function TAiChat.UploadFileToCache(aMediaFile: TAiMediaFile; aTTL_Seconds: integer = 3600): String;
begin
  // Permite subir archivos en caché,  este se puede trasladar entre diferentes peticiones del mismo chat
end;

{ TAOpeniChatMessage }

{
  procedure TAiOpenChatMessage.AddBase64Image(aBase64: String);
  begin
  VisionBase64.Add(aBase64);
  end;
}

procedure TAiChatMessage.AddMediaFile(aMediaFile: TAiMediaFile);
begin
  FMediaFiles.Add(aMediaFile);
end;

constructor TAiChatMessage.Create(aPrompt, aRole: String; aToolCallId: String = ''; aFunctionName: String = '');
begin
  Inherited Create;
  // FVisionUrls := TStringList.Create;
  // FVisionBase64 := TStringList.Create;
  Self.FRole := aRole;
  Self.FPrompt := aPrompt;
  Self.FFunctionName := aFunctionName;
  Self.FTollCallId := aToolCallId;
  FMediaFiles := TAiMediaFiles.Create;
  FWebSearchResponse := TAiWebSearch.Create;
  FPreviousResponseId := ''; // Inicializar la nueva propiedad
end;

destructor TAiChatMessage.Destroy;
begin
  FMediaFiles.Free;
  inherited;
end;

function TAiChatMessage.GetMediaTranscription: String;
Var
  MF: TAiMediaFile;
begin
  Result := '';

  for MF in MediaFiles do
  begin
    If MF.Procesado = True then
      Result := Trim(Result + sLineBreak + MF.Transcription);
  end;
end;

function TAiChatMessage.HasUnprocessedItems: Boolean;
Var
  MF: TAiMediaFile;
begin
  Result := False;

  for MF in MediaFiles do
  begin
    If MF.Procesado = False then
    Begin
      Result := True;
      Break;
    End;
  end;
end;

procedure TAiChatMessage.LoadMediaFromBase64(aFileName, aBase64: String);
Var
  Media: TAiMediaFile;
begin
  If Length(Trim(aBase64)) < 100 then
    Raise Exception.Create('El Base64 está vacío, no se cargará');

  If aFileName = '' then // Ver como se asigna un nombre a partir del contenido del stream
    aFileName := 'imagen.jpg';

  Media := TAiMediaFile.Create;
  Media.LoadFromBase64(aFileName, aBase64);

  AddMediaFile(Media);
end;

procedure TAiChatMessage.LoadMediaFromFile(aFileName: String);
Var
  Media: TAiMediaFile;
begin
  If Not FileExists(aFileName) then
    Raise Exception.Create('El archivo "' + aFileName + '" no se encuentra');

  Media := TAiMediaFile.Create;
  Media.LoadFromFile(aFileName);
  AddMediaFile(Media);
end;

procedure TAiChatMessage.LoadMediaFromStream(aFileName: String; Stream: TMemoryStream);
Var
  Media: TAiMediaFile;
begin
  If Stream.Size <= 100 then
    Raise Exception.Create('El stream está vacío');

  If aFileName = '' then // Ver como se asigna un nombre a partir del contenido del stream
    aFileName := 'imagen.jpg';

  Media := TAiMediaFile.Create;
  Media.LoadFromStream(aFileName, Stream);
  Self.AddMediaFile(Media);
end;

procedure TAiChatMessage.SetCompletion_tokens(const Value: integer);
begin
  FCompletion_tokens := Value;
end;

procedure TAiChatMessage.SetContent(const Value: String);
begin
  FContent := Value;
end;

procedure TAiChatMessage.SetFId(const Value: integer);
begin
  FId := Value;
end;

procedure TAiChatMessage.SetFPreviousResponseId(const Value: String);
begin
  FPreviousResponseId := Value;
end;

procedure TAiChatMessage.SetFunctionName(const Value: String);
begin
  FFunctionName := Value;
end;

procedure TAiChatMessage.SetIsTollCallResponse(const Value: Boolean);
begin
  FIsTollCallResponse := Value;
end;

procedure TAiChatMessage.SetPrompt(const Value: String);
begin
  FPrompt := Value;
end;

procedure TAiChatMessage.SetPrompt_tokens(const Value: integer);
begin
  FPrompt_tokens := Value;
end;

procedure TAiChatMessage.SetReasoning(const Value: String);
begin
  FReasoning := Value;
end;

procedure TAiChatMessage.SetRole(const Value: String);
begin
  FRole := Value;
end;

procedure TAiChatMessage.SetTollCallId(const Value: String);
begin
  FTollCallId := Value;
end;

procedure TAiChatMessage.SetTool_calls(const Value: String);
begin
  FTool_calls := Value;
end;

procedure TAiChatMessage.SetTotal_tokens(const Value: integer);
begin
  FTotal_tokens := Value;
end;

procedure TAiChatMessage.SetWebSearchResponse(const Value: TAiWebSearch);
begin
  FWebSearchResponse := Value;
end;

{ procedure TAiOpenChatMessage.SetVisionBase64(const Value: TStringList);
  begin
  FVisionBase64.Text := Value.Text;
  end;

  procedure TAiOpenChatMessage.SetVisionUrls(const Value: TStringList);
  begin
  FVisionUrls.Text := Value.Text;
  end;
}

function TAiChatMessage.StreamToBase64(Stream: TMemoryStream): String;
begin
  Stream.Position := 0;
  Result := TNetEncoding.Base64.EncodeBytesToString(Stream.Memory, Stream.Size);
end;

function TAiChatMessage.ToJSon: TJSonArray;
Var
  Msg: TAiChatMessage;
  jObj, JMsg: TJSonObject;
  JContent: TJSonArray;
  ImagePayload: TStringStream;
  Base64, Mime: String;
  MediaArr: TAiMediaFilesArray;
  S: String;
begin
  // Esta función solo toma el mensaje actual y una sola imágen, la primera que encuentra en la lista
  // Esto se hace especialmente para modelos que solo aceptan una imágen por petición y no un chat completo

  Result := TJSonArray.Create;

  Msg := Self;
  jObj := TJSonObject.Create;
  jObj.AddPair('role', Msg.FRole);

  If Msg.FTollCallId <> '' then
    jObj.AddPair('tool_call_id', Msg.FTollCallId);

  If Msg.FFunctionName <> '' then
    jObj.AddPair('name', Msg.FFunctionName);

  // de todos los archivos de medios selecciona las imágenes que es lo que podemos manejar por ahora
  // y las imágenes que no han sigo preprocesadas, por si el modelo no maneja imagenes, previamente
  // se deben haber procesado en en el momendo de adicionar el mensaje al chat
  MediaArr := Msg.MediaFiles.GetMediaList([Tfc_Image], False);

  If (Length(MediaArr) > 0) then
  Begin

    JContent := TJSonArray.Create;
    JMsg := TJSonObject.Create;
    JMsg.AddPair('type', 'text');
    JMsg.AddPair('text', Msg.FPrompt);
    JContent.Add(JMsg);

    If Msg.MediaFiles.Count > 0 then // Solo toma la primera imagen
    Begin
      Base64 := Msg.MediaFiles[0].Base64;
      Mime := Msg.MediaFiles[0].MimeType;

      ImagePayload := TStringStream.Create('{"type": "image_url", "image_url": {"url": "data:' + Mime + ';base64,' + Base64 + '"}}', TEncoding.UTF8);
      S := ImagePayload.DataString;
      try
        JContent.Add(TJSonObject.ParseJSONValue(ImagePayload.DataString) as TJSonObject);
      finally
        ImagePayload.Free;
      end;

    End;
    jObj.AddPair('content', JContent);
  End
  Else
  Begin

    jObj.AddPair('content', Msg.FPrompt);
  End;

  If Msg.FTool_calls <> '' then

{$IF CompilerVersion < 35}
    jObj.AddPair('tool_calls', TJSONUtils.ParseAsArray(Msg.FTool_calls));
{$ELSE}
    jObj.AddPair('tool_calls', TJSonArray(TJSonArray.ParseJSONValue(Msg.FTool_calls)));
{$ENDIF}
  Result.Add(jObj);
end;

function TAiChatMessages.ExportChatHistory: TJSonObject;
Var
  jObj, JItem: TJSonObject;
  JArr: TJSonArray;
  I: integer;
  Item: TAiChatMessage;
begin
  jObj := TJSonObject.Create;
  JArr := TJSonArray.Create;

  Try
    jObj.AddPair('model', 'MakerAiChat');
    jObj.AddPair('type', 'Messages');
    jObj.AddPair('ver', '1.0');

    For I := 0 to Self.Count - 1 do
    Begin
      Item := Self.Items[I];
      JItem := TJSonObject.Create;

      JItem.AddPair('role', Item.Role);
      If Trim(Item.Content) <> '' then
        JItem.AddPair('request', Item.Content)
      Else
        JItem.AddPair('prompt', Item.Prompt);
      JArr.Add(JItem);
    End;
    jObj.AddPair('data', JArr);
    Result := jObj;

  Finally
    // jObj.Free;
  End;
end;

{ TAiOpenChatMessages }

function TAiChatMessages.GetAsText: String;
Var
  St: TStringStream;
begin
  St := TStringStream.Create('', TEncoding.UTF8);
  Try
    SaveToStream(St);
    Result := St.DataString;
  Finally
    St.Free;
  End;
end;

procedure TAiChatMessages.LoadFromFile(FileName: String);
Var
  St: TStringStream;
begin
  St := TStringStream.Create('', TEncoding.UTF8);
  Try
    If FileExists(FileName) then
    Begin
      St.LoadFromFile(FileName);
      St.Position := 0;
      LoadFromStream(St);
    End;
  Finally
    St.Free;
  End;
end;

procedure TAiChatMessages.LoadFromStream(Stream: TStream);
Var
  jObj, JItem: TJSonObject;
  JArr: TJSonArray;
  sJson, Model, S: String;
  St: TStringStream;
  Item: TAiChatMessage;
  I: integer;
begin
  St := TStringStream.Create('', TEncoding.UTF8);
  Try
    St.LoadFromStream(Stream);
    sJson := St.DataString;

    jObj := TJSonObject(TJSonObject.ParseJSONValue(sJson));

    If Assigned(jObj) and (jObj.TryGetValue<String>('model', Model)) then
    Begin
      If Model = 'AiOpenChat' then
      Begin
        JArr := TJSonArray(jObj.GetValue<TJSonArray>('data'));

        S := JArr.Format;
        If JArr.Count > 0 then
          Self.Clear;

        For I := 0 to JArr.Count - 1 do
        Begin

{$IF CompilerVersion < 35}
          JItem := JArr.GetItemAsObject(I);
{$ELSE}
          JItem := TJSonObject(JArr[I]);
{$ENDIF}
          Item := TJSon.JsonToObject<TAiChatMessage>(JItem);
          Self.Add(Item);
        End;
      End;
    End;
  Finally
    St.Free;
    FreeAndNil(jObj);
  End;
end;

procedure TAiChatMessages.SaveToFile(FileName: String);
Var
  St: TStringStream;
begin
  St := TStringStream.Create('', TEncoding.UTF8);
  Try
    Self.SaveToStream(St);
    St.Position := 0;
    St.SaveToFile(FileName);
  Finally
    St.Free;
  End;
end;

procedure TAiChatMessages.SaveToStream(Stream: TStream);
Var
  jObj, JItem: TJSonObject;
  JArr: TJSonArray;
  St: TStringStream;
  I: integer;
  Item: TAiChatMessage;
begin
  St := TStringStream.Create('', TEncoding.UTF8);
  jObj := TJSonObject.Create;
  JArr := TJSonArray.Create;

  Try
    jObj.AddPair('model', 'MakerAiChat');
    jObj.AddPair('type', 'Messages');
    jObj.AddPair('ver', '1.0');

    For I := 0 to Self.Count - 1 do
    Begin
      Item := Self.Items[I];
      JItem := TJSon.ObjectToJsonObject(Item);
      JArr.Add(JItem);
    End;

    jObj.AddPair('data', JArr);
    St.WriteString(jObj.Format);
    St.SaveToStream(Stream);
  Finally
    St.Free;
    jObj.Free;
  End;
end;

procedure TAiChatMessages.SetAsText(const Value: String);
Var
  St: TStringStream;
begin
  St := TStringStream.Create('', TEncoding.UTF8);
  Try
    If Trim(Value) <> '' then
    Begin
      St.WriteString(Value);
      St.Position := 0;
      LoadFromStream(St);
    End;
  Finally
    St.Free;
  End;
end;

procedure TAiChatMessages.SetNativeInputFiles(const Value: TAiFileCategories);
begin
  FNativeInputFiles := Value;
end;

function TAiChatMessages.ToJSon: TJSonArray;
Var
  I, J: integer;
  Msg: TAiChatMessage;
  jObj, JMsg: TJSonObject;
  JObjImg, jImgUrl, jAudio: TJSonObject;
  JContent: TJSonArray;
  Base64, Mime: String;
  MediaArr: TAiMediaFilesArray;
  S: String;
  MediaFile: TAiMediaFile;
begin
  Result := TJSonArray.Create;

  For I := 0 to Count - 1 do
  Begin
    Msg := Self.Items[I];
    jObj := TJSonObject.Create;
    jObj.AddPair('role', Msg.FRole);

    If Msg.FTollCallId <> '' then
      jObj.AddPair('tool_call_id', Msg.FTollCallId);

    If Msg.FFunctionName <> '' then
      jObj.AddPair('name', Msg.FFunctionName);

    // de todos los archivos de medios selecciona las imágenes que es lo que podemos manejar por ahora
    // y las imágenes que no han sigo preprocesadas, por si el modelo no maneja imagenes, previamente
    // se deben haber procesado en en el momento de adicionar el mensaje al chat
    MediaArr := Msg.MediaFiles.GetMediaList(FNativeInputFiles, False);

    If (Length(MediaArr) > 0) then
    Begin

      JContent := TJSonArray.Create;
      JMsg := TJSonObject.Create;
      JMsg.AddPair('type', 'text');
      JMsg.AddPair('text', Msg.FPrompt);
      JContent.Add(JMsg);

      For J := 0 to Length(MediaArr) - 1 do // Open Ai permite subir el Base64 o el Url, siempre se sube el Base64, por estandar
      Begin
        MediaFile := MediaArr[J];

        Case MediaFile.FileCategory of
          TAiFileCategory.Tfc_Image:
            Begin
              Base64 := MediaFile.Base64;
              Mime := MediaFile.MimeType;

              // Esta es otra forma de hacer lo mismo con json directamente
              S := 'data:' + Mime + ';base64,' + Base64;

              jImgUrl := TJSonObject.Create;
              jImgUrl.AddPair('url', S);

              If Msg.MediaFiles[J].Detail <> '' then // Si define high or low.
                jImgUrl.AddPair('detail', MediaFile.Detail);

              JObjImg := TJSonObject.Create;
              JObjImg.AddPair('type', 'image_url');
              JObjImg.AddPair('image_url', jImgUrl);

              JContent.Add(JObjImg);

              jObj.AddPair('content', JContent);

            End;
          TAiFileCategory.Tfc_Audio:
            Begin
              If MediaFile.IdAudio <> '' then // Si es una respuesta del modelo va esto
              Begin
                jAudio := TJSonObject.Create;
                jAudio.AddPair('id', MediaFile.IdAudio);
                jObj.AddPair('audio', jAudio);
              End
              Else // Si es un audio del usuario va esto
              Begin
                jAudio := TJSonObject.Create;
                jAudio.AddPair('data', MediaFile.Base64);
                jAudio.AddPair('format', StringReplace(MediaFile.MimeType, 'audio/', '', [rfReplaceAll]));

                JContent := TJSonArray.Create;
                JMsg := TJSonObject.Create;
                JMsg.AddPair('type', 'input_audio');
                JMsg.AddPair('input_audio', jAudio);
                JContent.Add(JMsg);

                jObj.AddPair('content', JContent);
              End;
            End;

          TAiFileCategory.Tfc_Video:
            Begin
            end;
          TAiFileCategory.Tfc_pdf: // El completions no maneja pdf todavía hay que usar el Responses
            Begin
              {
                var
                JDocumentObject := TJSonObject.Create;
                JDocumentObject.AddPair('type', 'input_file');
                JDocumentObject.AddPair('filename', MediaFile.FileName);

                // Por facilidad no manejamos el ID del documento solo subimos el Base64
                // Podría adicionarse también el manejo de archivos por los vectores de openAi
                // Asignar directamente el base64 como cadena, no como objeto
                if MediaFile.UrlMedia <> '' then
                Raise Exception.Create('En documentos no se maneja la url') // JDocumentObject.AddPair('image_url', MediaFile.UrlMedia)
                else
                JDocumentObject.AddPair('file_data', 'data:' + MediaFile.MimeType + ';base64,' + MediaFile.Base64);

                // Si tiene la propiedad Detail, la agregamos
                if MediaFile.Detail <> '' then
                JDocumentObject.AddPair('detail', MediaFile.Detail);

                JContent.Add(JDocumentObject);
              }
            end;
          TAiFileCategory.Tfc_Document:
            Begin
            end;
          TAiFileCategory.Tfc_Text:
            Begin
            end;
          TAiFileCategory.Tfc_CalcSheet:
            Begin
            end;
          TAiFileCategory.Tfc_Presentation:
            Begin
            end;
          TAiFileCategory.Tfc_CompressFile:
            Begin
            end;
          TAiFileCategory.Tfc_Web:
            Begin
            end;
          TAiFileCategory.Tfc_GraphicDesign:
            Begin
            end;
          TAiFileCategory.Tfc_Unknow:
            Begin
            end;

        Else
          Begin
            jObj.AddPair('content', Msg.FPrompt);
          End;
        End;
      End;
      If Msg.FTool_calls <> '' then

{$IF CompilerVersion < 35}
        jObj.AddPair('tool_calls', TJSONUtils.ParseAsArray(Msg.FTool_calls));
{$ELSE}
        jObj.AddPair('tool_calls', TJSonArray(TJSonArray.ParseJSONValue(Msg.FTool_calls)));
{$ENDIF}



      // Result.Add(jObj);

    End
    Else // Si no tiene archivos de medios simplemente envía el prompt
    Begin
      jObj.AddPair('content', Msg.FPrompt);
    End;

    If Msg.FTool_calls <> '' then

{$IF CompilerVersion < 35}
      jObj.AddPair('tool_calls', TJSONUtils.ParseAsArray(Msg.FTool_calls));
{$ELSE}
      jObj.AddPair('tool_calls', TJSonArray(TJSonArray.ParseJSONValue(Msg.FTool_calls)));
{$ENDIF}
    Result.Add(jObj);
  end;

end;

end.
